/// Container for Key Measures
table '_Key Measures'
	lineageTag: 73c099da-2cd7-46c3-b43b-fb93a5325ea2

	measure 'Service Contract Gross Profit' = [Service Contract Revenue] - [Service Contract Cost]
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 03 Gross Profit\Service Contract
		lineageTag: bcd20ffd-c100-434d-aa6e-6549b9d2337a

	measure 'Service Contract Gross Profit %' =
			DIVIDE(
						[Service Contract Gross Profit],
						[Service Contract Revenue]
					)
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: 03 Gross Profit\Service Contract
		lineageTag: 87124807-180c-4643-ac35-25a4b89b535c

	measure 'Project Revenue' = CALCULATE( SUM(PROJECT_REVENUE_F[Amount]), 'FYCALENDAR_D'[Is Completed Month] = TRUE )
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 00 Base\Revenue\Project
		lineageTag: 11d33cb6-f59b-48e7-ad4c-2266a9d5aae7

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'Project Cost' = CALCULATE( SUM(PROJECT_COSTS_F[Amount]), 'FYCALENDAR_D'[Is Completed Month] = TRUE )
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 00 Base\Cost\Project
		lineageTag: f0db1013-4217-4b9d-9dc8-68dce3253c16

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'Project GP' = [Project Revenue] - [Project Cost]
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 03 Gross Profit\Project
		lineageTag: 7426006b-d267-4b2c-8a66-b77aa212edad

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'Project GP %' =
			DIVIDE(
						[Project GP],
						[Project Revenue]
					)
		displayFolder: 03 Gross Profit\Project
		lineageTag: a8d780d5-3605-461a-b9d1-8959b2639502

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Project GP % (Formatted)' =
			FORMAT(
						[Project GP %],
						"0.0%"
					)
		displayFolder: 90 Debug\Formatting
		lineageTag: 11f33b54-c29e-4580-80cd-04d085385733

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Service Contract Cost' = CALCULATE( SUM(SERVCONTRACTS_COSTS_F[Amount]), 'FYCALENDAR_D'[Is Completed Month] = TRUE )
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 00 Base\Cost\Service Contract
		lineageTag: e5f87648-4ace-4269-a9c5-78fb3267c6eb

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Service Contract Revenue' = CALCULATE( SUM(SERVCONTRACTS_REVENUE_F[Amount]), 'FYCALENDAR_D'[Is Completed Month] = TRUE )
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 00 Base\Revenue\Service Contract
		lineageTag: 644417d4-502d-401d-9320-1b21d78bcbf0

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure Check_Source_Calls = SUM('CALLS_F'[Cost All])
		displayFolder: 99 Checks
		lineageTag: 56cdc3a4-1e10-4d8a-8e86-eaaa2fe2b949

	measure Check_Dest_Total =
			CALCULATE(
						SUM('PROJECT_COSTS_F'[Amount]),
						'PROJECT_COSTS_F'[Source Type] = "Service Call Cost"
					) + SUM('SERVCONTRACTS_COSTS_F'[Amount]) + SUM('SPOT_FINANCIALS_F'[Cost])
		displayFolder: 99 Checks
		lineageTag: 642593c2-8adc-4117-a65a-bb83ac3b9d88

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure Check_Variance = [Check_Source_Calls] - [Check_Dest_Total]
		displayFolder: 99 Checks
		lineageTag: b49f0e53-e383-467c-9f39-9b1398f92ff7

	measure 'T&M Revenue' = CALCULATE( SUM(SPOT_FINANCIALS_F[Revenue]), 'FYCALENDAR_D'[Is Completed Month] = TRUE )
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 00 Base\Revenue\T&M
		lineageTag: 81832730-264e-4837-a2c5-84118306ae8b

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'T&M Gross Profit' = [T&M Revenue] - [T&M Cost]
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 03 Gross Profit\T&M
		lineageTag: 4829bbf6-a689-43c1-97a3-46b14da9f169

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'T&M GP %' =
			DIVIDE(
						[T&M Gross Profit],
						[T&M Revenue]
					)
		displayFolder: 03 Gross Profit\T&M
		lineageTag: 646a9f17-5fa5-4c16-ad83-7aed27b518ca

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure Check_Source_Revenue_Total =
			VAR JobRevenue = SUM('JOB_MONTHLY_SUMMARY_F'[Contract Earned Curr Mo])
					VAR ContractBillingRevenue = SUM('CONTRACT_BILLABLE_F'[BILLABLE_ALL])
					VAR SpotCallsRevenue = CALCULATE(
						SUM('CALLS_F'[Billable All]),
						'CALLS_F'[Contract Number] = BLANK() || 'CALLS_F'[Contract Number] = ""
					)
			
					RETURN
						JobRevenue + ContractBillingRevenue + SpotCallsRevenue
		displayFolder: 99 Checks
		lineageTag: 4176eace-9262-4929-a9c0-0acdda477a1a

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure Check_Dest_Revenue_Total =
			VAR ProjectRev = SUM('PROJECT_REVENUE_F'[Amount])
					VAR ContractRev = SUM('SERVCONTRACTS_REVENUE_F'[Amount])
					VAR SpotRev = SUM('SPOT_FINANCIALS_F'[Revenue])
			
					RETURN
						ProjectRev + ContractRev + SpotRev
		displayFolder: 99 Checks
		lineageTag: 9ac4048d-167e-466a-81ed-51199e7afad4

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure Check_Revenue_Variance = [Check_Source_Revenue_Total] - [Check_Dest_Revenue_Total]
		displayFolder: 99 Checks
		lineageTag: bcfca2b0-c232-4578-8f83-e92ea72b887f

	measure 'T&M Cost' = CALCULATE( SUM(SPOT_FINANCIALS_F[Cost]), 'FYCALENDAR_D'[Is Completed Month] = TRUE )
		displayFolder: 00 Base\Cost\T&M
		lineageTag: b5f1eb04-bf53-4fa9-a7be-74edd99a4956

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Total Revenue' = [Project Revenue] + [Service Contract Revenue] + [T&M Revenue]
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 01 Revenue
		lineageTag: 74e4a287-d2f3-4124-a374-b35d0902dc23

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'Total Cost' = [Project Cost] + [Service Contract Cost] + [T&M Cost]
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 02 Cost
		lineageTag: 22730745-e9b9-465b-a13b-f35ed90e1e1c

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'Total Gross Profit' = [Total Revenue] - [Total Cost]
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 03 Gross Profit\Total
		lineageTag: 831d0b0f-bd15-41af-9a82-e10ad578a581

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'Total GP %' =
			DIVIDE(
						[Total Gross Profit],
						[Total Revenue]
					)
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: 03 Gross Profit\Total
		lineageTag: 57e3fb96-50e3-4df4-a923-18eaf349a3bb

	measure 'Has Revenue or Cost' =
			VAR Rev = [Total Revenue]
					VAR Cost = [Total Cost]
					RETURN
						IF(
							COALESCE(
								Rev,
								0
							) <> 0
							|| COALESCE(
								Cost,
								0
							) <> 0,
							1,
							0
						)
		displayFolder: 90 Debug\Helpers
		lineageTag: 458d0ba9-34c2-4f10-bf6b-f31a56040de7

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Revenue (By Stream)' = ```
			
			VAR Stream =
			    SELECTEDVALUE ( Revenue_Stream_D[Revenue Stream] )
			RETURN
			    IF (
			        ISBLANK ( Stream ),
			        [Total Revenue],
			        SWITCH (
			            Stream,
			            "Project", [Project Revenue],
			            "Service Contract", [Service Contract Revenue],
			            "T&M", [T&M Revenue]
			        )
			    )
			
			```
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 01 Revenue\By Stream
		lineageTag: 1143cebd-9853-4368-a5c5-4b98e773fee3

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'Cost (By Stream)' = ```
			
			VAR Stream =
			    SELECTEDVALUE ( Revenue_Stream_D[Revenue Stream] )
			RETURN
			    IF (
			        ISBLANK ( Stream ),
			        [Total Cost],
			        SWITCH (
			            Stream,
			            "Project", [Project Cost],
			            "Service Contract", [Service Contract Cost],
			            "T&M", [T&M Cost]
			        )
			    )
			
			```
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 02 Cost\By Stream
		lineageTag: bf013bef-018a-4f8d-8bb8-1b1a261c014c

	measure 'Gross Profit (By Stream)' = ```
			
			        [Revenue (By Stream)] - [Cost (By Stream)]
			```
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 03 Gross Profit\By Stream
		lineageTag: 99980665-1dcc-476a-9910-f98c9afa3bcc

	measure 'GP % (By Stream)' = ```
			
			        DIVIDE (
			            [Gross Profit (By Stream)],
			            [Revenue (By Stream)]
			        )
			```
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: 03 Gross Profit\By Stream
		lineageTag: 657b6252-7cd8-439b-807d-a4b8f0f0b969

	measure 'Revenue % of Total' = ```
			
			        DIVIDE (
			            [Revenue (By Stream)],
			            [Total Revenue]
			        )
			```
		displayFolder: 01 Revenue\By Stream
		lineageTag: 1c5e3daa-7b9c-4e14-975c-2fadcd35789d

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Cost % of Total' = ```
			
			        DIVIDE (
			            [Cost (By Stream)],
			            [Total Cost]
			        )
			```
		displayFolder: 02 Cost\By Stream
		lineageTag: 5efb6daf-e1c8-4cb9-a859-a258310a901f

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'GP % of Total Revenue' = ```
			
			        DIVIDE (
			            [Gross Profit (By Stream)],
			            [Total Revenue]
			        )
			```
		displayFolder: 03 Gross Profit\By Stream
		lineageTag: ca281b1e-0e08-4bd8-a66c-47cd48f40586

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Prior Year Service Contract Revenue for comparison.
	measure 'Service Contract Revenue PY (FY Period)' = CALCULATE([Service Contract Revenue], SAMEPERIODLASTYEAR('FYCALENDAR_D'[Date]))
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 04 Time Intelligence – Snapshot
		lineageTag: 85542245-c109-45f3-a0fb-8052ddc70c03

	/// Total revenue from the prior period for customers who existed in the prior period. Used as the denominator for retention rates.
	measure 'Retention Base PY' = ```
			
			            SUMX(
			                VALUES(CUSTOMERS_D[Customer Key]),
			                VAR RevenuePY = [Service Contract Revenue PY (FY Period)]
			                RETURN IF(RevenuePY > 0, RevenuePY, 0)
			            )
			```
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 04 Time Intelligence – Snapshot\Components
		lineageTag: b9c0bf6f-0b36-402a-9dc5-323eab1051f0

	/// Revenue lost from customers who contributed in the prior period but have 0 revenue in the current period.
	measure 'Churn Revenue (FY Period)' = ```
			
			            SUMX(
			                VALUES(CUSTOMERS_D[Customer Key]),
			                VAR RevenueCY = [Service Contract Revenue]
			                VAR RevenuePY = [Service Contract Revenue PY (FY Period)]
			                RETURN IF(RevenuePY > 0 && RevenueCY = 0, RevenuePY, 0)
			            )
			```
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 04 Time Intelligence – Snapshot\Components
		lineageTag: fd625eaf-de3b-4a35-91c5-8bfc880fc291

	/// The decrease in revenue for customers who exist in both periods (Revenue CY < Revenue PY).
	measure 'Contraction Revenue (FY Period)' = ```
			
			            SUMX(
			                VALUES(CUSTOMERS_D[Customer Key]),
			                VAR RevenueCY = [Service Contract Revenue]
			                VAR RevenuePY = [Service Contract Revenue PY (FY Period)]
			                RETURN IF(RevenuePY > 0 && RevenueCY > 0 && RevenueCY < RevenuePY, RevenuePY - RevenueCY, 0)
			            )
			```
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 04 Time Intelligence – Snapshot\Components
		lineageTag: 7d2cb635-df4f-4df4-b3b0-22768897f146

	/// The increase in revenue for customers who exist in both periods (Revenue CY > Revenue PY).
	measure 'Upsell Revenue (FY Period)' = ```
			
			            SUMX(
			                VALUES(CUSTOMERS_D[Customer Key]),
			                VAR RevenueCY = [Service Contract Revenue]
			                VAR RevenuePY = [Service Contract Revenue PY (FY Period)]
			                RETURN IF(RevenuePY > 0 && RevenueCY > RevenuePY, RevenueCY - RevenuePY, 0)
			            )
			```
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 04 Time Intelligence – Snapshot\Components
		lineageTag: 1fe3e531-8fdd-412b-9610-c373fdff7255

	/// Net Revenue Retention (NRR). Measures the total value of renewed contracts plus expansion revenue minus churn and contraction. Target > 100%.
	measure 'Net Revenue Retention % (FY Period)' = ```
			
			            DIVIDE(
			                [Retention Base PY] - [Churn Revenue (FY Period)] - [Contraction Revenue (FY Period)]  + [Upsell Revenue (FY Period)],
			                [Retention Base PY]
			            )
			```
		formatString: 0.0%;-0.0%;0.0%
		displayFolder: 06 Retention KPIs – Snapshot
		lineageTag: ac4c387d-72d1-4688-a447-901d21f33d31

	/// Gross Revenue Retention (GRR). Measures the retained value of renewed contracts, excluding upsells. Max is 100%.
	measure 'Gross Revenue Retention % (FY Period)' = ```
			
			            DIVIDE(
			                [Retention Base PY] - [Churn Revenue (FY Period)] - [Contraction Revenue (FY Period)],
			                [Retention Base PY]
			            )
			```
		formatString: 0.0%;-0.0%;0.0%
		displayFolder: 06 Retention KPIs – Snapshot
		lineageTag: a4c4b8b6-3728-436c-9761-f1c95c9f6d87

	/// The percentage of revenue lost due to churned customers.
	measure 'Churn Rate % (FY Period)' = DIVIDE([Churn Revenue (FY Period)], [Retention Base PY])
		formatString: 0.0%;-0.0%;0.0%
		displayFolder: 06 Retention KPIs – Snapshot
		lineageTag: 05f6ca58-84a2-437b-a316-0f1950c1bad9

	/// Rolling 12 Month Revenue for the Current Period.
	measure 'Service Contract Revenue (TTM)' = ```
			
			VAR RawAnchorDate =
			    MAX ( SERVCONTRACTS_REVENUE_F[Date] )
			
			VAR AnchorDate =
			    EOMONTH ( RawAnchorDate, 0 )
			RETURN
			CALCULATE (
			    [Service Contract Revenue],
			    DATESINPERIOD (
			        FYCALENDAR_D[Date],
			        AnchorDate,
			        -12,
			        MONTH
			    )
			)
			
			```
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 90 Debug\Legacy\TTM
		lineageTag: a1ba7d1e-8bb8-4941-83be-673ec5bd1b31

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Rolling 12 Month Revenue for the Prior Period (13-24 months ago), using EDATE for cleaner logic.
	measure 'Service Contract Revenue PY (TTM)' = ```
			
			VAR RawAnchorDate =
			    MAX ( SERVCONTRACTS_REVENUE_F[Date] )
			
			VAR AnchorDate =
			    EOMONTH ( RawAnchorDate, 0 )
			RETURN
			IF (
			    ISBLANK ( AnchorDate ),
			    BLANK(),
			    CALCULATE (
			        [Service Contract Revenue],
			        DATESINPERIOD (
			            FYCALENDAR_D[Date],
			            EDATE ( AnchorDate, -12 ),
			            -12,
			            MONTH
			        )
			    )
			)
			
			```
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 90 Debug\Legacy\TTM
		lineageTag: 494f829e-d4d6-450c-8fe6-0fbb0fd6713f

	/// TTM Denominator: Prior 12-month revenue for customers who existed in that period.
	measure 'Retention Base PY (TTM)' = ```
			
			CALCULATE (
			    [Service Contract Revenue PY (TTM)],
			    KEEPFILTERS ( SERVCONTRACTS_REVENUE_F[Date] )
			)
			
			```
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 90 Debug\Legacy\TTM
		lineageTag: 1c8a62a2-d097-4c91-8a4c-b7227861a823

	/// TTM Churn: Lost revenue from customers who had revenue in PY (TTM) but 0 in CY (TTM).
	measure 'Churn Revenue (TTM)' = ```
			
			VAR RevCY =
			    [Service Contract Revenue (TTM)]
			VAR RevPY =
			    [Service Contract Revenue PY (TTM)]
			RETURN
			IF (
			    RevPY > 0
			        && ( ISBLANK ( RevCY ) || RevCY = 0 ),
			    RevPY,
			    BLANK()
			)
			
			```
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 90 Debug\Legacy\TTM
		lineageTag: e20d31ca-c878-4035-aa4a-a91da8275456

	/// TTM Contraction: Decrease in revenue for retained customers over the rolling period.
	measure 'Contraction Revenue (TTM)' = ```
			
			VAR RevCY =
			    [Service Contract Revenue (TTM)]
			VAR RevPY =
			    [Service Contract Revenue PY (TTM)]
			RETURN
			IF (
			    RevPY > 0
			        && RevCY > 0
			        && RevCY < RevPY,
			    RevPY - RevCY,
			    BLANK()
			)
			
			```
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 90 Debug\Legacy\TTM
		lineageTag: 564d13e6-d846-4007-9223-e4583634af65

	/// TTM Upsell: Increase in revenue for retained customers over the rolling period.
	measure 'Upsell Revenue (TTM)' = ```
			
			VAR RevCY =
			    [Service Contract Revenue (TTM)]
			VAR RevPY =
			    [Service Contract Revenue PY (TTM)]
			RETURN
			IF (
			    RevPY > 0
			        && RevCY > RevPY,
			    RevCY - RevPY,
			    BLANK()
			)
			
			```
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 90 Debug\Legacy\TTM
		lineageTag: 21d23976-54fb-4534-91d9-9ee27c52616c

	/// Net Revenue Retention (TTM). Rolling 12-month view.
	measure 'Net Revenue Retention % (TTM)' = ```
			
			            DIVIDE(
			                [Retention Base PY (TTM)] - [Churn Revenue (TTM)] - [Contraction Revenue (TTM)] + [Upsell Revenue (TTM)],
			                [Retention Base PY (TTM)]
			            )
			```
		formatString: 0.0%;-0.0%;0.0%
		displayFolder: 90 Debug\Legacy\TTM
		lineageTag: 0abb25da-222b-4dbe-be8f-0cc4fa568a12

	/// Churn Rate (TTM). Rolling 12-month view.
	measure 'Churn Rate % (TTM)' = ```
			
			DIVIDE (
			    [Churn Revenue (TTM)],
			    [Retention Base PY (TTM)]
			)
			
			```
		formatString: 0.0%;-0.0%;0.0%
		displayFolder: 90 Debug\Legacy\TTM
		lineageTag: bccaf7de-19e2-414f-87ee-1ad06e2007c4

	measure 'Gross Revenue Retention % (TTM)' = ```
			
			VAR RevCY =
			    [Service Contract Revenue (TTM)]
			VAR RevPY =
			    [Service Contract Revenue PY (TTM)]
			RETURN
			DIVIDE (
			    MIN ( RevCY, RevPY ),
			    RevPY
			)
			
			```
		formatString: 0.0%;-0.0%;0.0%
		displayFolder: 90 Debug\Legacy\TTM
		lineageTag: 0c226a0b-d5cd-4e0d-8b8c-4cf98ac24103

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Debug – Anchor Date' = ```
			
			MAX ( SERVCONTRACTS_REVENUE_F[Date] )
			
			```
		displayFolder: 90 Debug
		lineageTag: 2e70543d-4716-4e01-821a-59c40e8b8ff8

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Customers Churned (TTM)' = ```
			
			CALCULATE (
			    DISTINCTCOUNT ( CUSTOMERS_D[Customer Key] ),
			    FILTER (
			        VALUES ( CUSTOMERS_D[Customer Key] ),
			        [Service Contract Revenue PY (TTM)] > 0
			            && (
			                ISBLANK ( [Service Contract Revenue (TTM – As Of Today)] )
			                || [Service Contract Revenue (TTM – As Of Today)] = 0
			            )
			    )
			)
			
			```
		displayFolder: 90 Debug\Legacy\TTM
		lineageTag: 419cbdd0-491d-4ecb-b888-f5e7e28b7ed8

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Logo Churn Rate % (TTM)' = ```
			
			DIVIDE (
			    [Customers Churned (TTM)],
			    [Customers PY (TTM)]
			)
			
			```
		formatString: 0.0%;-0.0%;0.0%
		displayFolder: 90 Debug\Legacy\TTM
		lineageTag: 8fa89ecc-95f2-48cd-9f88-24926b0189ef

	measure 'Customers PY (TTM)' = ```
			
			CALCULATE (
			    DISTINCTCOUNT ( CUSTOMERS_D[Customer Key] ),
			    FILTER (
			        VALUES ( CUSTOMERS_D[Customer Key] ),
			        [Service Contract Revenue PY (TTM)] > 0
			    )
			)
			
			```
		displayFolder: 90 Debug\Legacy\TTM
		lineageTag: 3ccaa1e3-da10-4521-b674-a7c0fc64906d

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Customers Inactive 12+ Months' = ```
			
			CALCULATE (
			    DISTINCTCOUNT ( CUSTOMERS_D[Customer Key] ),
			    FILTER (
			        VALUES ( CUSTOMERS_D[Customer Key] ),
			        VAR LastBilling = [Last Billing Date (Any Stream)]
			        RETURN
			            NOT ISBLANK ( LastBilling )
			                && DATEDIFF ( LastBilling, EOMONTH ( TODAY(), 0 ), MONTH ) >= 12
			    )
			)
			
			```
		displayFolder: 08 Customer Status\Counts
		lineageTag: 1775fa57-562f-4a85-9eff-4fe0967be43a

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Service Contract Revenue (TTM – As Of Today)' = ```
			
			VAR AnchorDate =
			    EOMONTH ( TODAY (), 0 )
			VAR Rev =
			    CALCULATE (
			        [Service Contract Revenue],
			        DATESINPERIOD (
			            FYCALENDAR_D[Date],
			            AnchorDate,
			            -12,
			            MONTH
			        )
			    )
			RETURN
			IF ( Rev > 0, Rev, BLANK () )
			
			```
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 90 Debug\Legacy\TTM
		lineageTag: c52a2f2d-8eee-4679-a798-ca684110c00d

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'NRR (TTM – Active Cohort Only)' = ```
			CALCULATE (
			    DIVIDE (
			        [Service Contract Revenue (TTM)],
			        [Service Contract Revenue PY (TTM)]
			    ),
			    FILTER (
			        VALUES ( CUSTOMERS_D[Customer Key] ),
			        DATEDIFF (
			            CALCULATE ( MAX ( SERVCONTRACTS_REVENUE_F[Date] ) ),
			            EOMONTH ( TODAY(), 0 ),
			            MONTH
			        ) <= 12
			    )
			)
			
			```
		formatString: 0.0%;-0.0%;0.0%
		displayFolder: 90 Debug\Legacy\TTM
		lineageTag: 6b8aa630-eb8d-456f-8f08-50b41b396388

	measure 'Months Since Last Contract Billing' = ```
			
			VAR LastBillingDate =
			    CALCULATE (
			        MAX ( SERVCONTRACTS_REVENUE_F[Date] )
			    )
			VAR AnchorDate =
			    EOMONTH ( TODAY (), 0 )
			RETURN
			IF (
			    ISBLANK ( LastBillingDate ),
			    BLANK (),
			    DATEDIFF (
			        EOMONTH ( LastBillingDate, 0 ),
			        AnchorDate,
			        MONTH
			    )
			)
			
			```
		displayFolder: 90 Debug\Legacy
		lineageTag: 9ca58e51-f9f4-4a31-baca-c72e9302a5f3

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Customer Contract Status' = ```
			
			IF (
			    [Months Since Last Contract Billing] >= 12,
			    "Churned",
			    "Active"
			)
			
			```
		displayFolder: 90 Debug\Legacy
		lineageTag: 9e4453a2-fdff-4ab1-ac09-2d639924e0bb

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Selected Customer Name' =
			
			IF (
			    ISFILTERED ( 'CUSTOMERS_D'[Customer Name] ),
			    IF (
			        HASONEVALUE ( 'CUSTOMERS_D'[Customer Name] ),
			        VALUES ( 'CUSTOMERS_D'[Customer Name] ),
			        "Multiple Customers Selected"
			    ),
			    "All Customers (Company Total)"
			)
		displayFolder: 90 Debug\UI
		lineageTag: 49325a64-8ed9-40f3-bbb5-437363f783fe

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Last Billing Date (Any Stream)' = ```
			
			CALCULATE (
			    MAX ( ALL_BILLINGS_F[BillingDate] ),
			    ALL_BILLINGS_F[Billing Amount] <> 0
			)
			
			```
		displayFolder: 08 Customer Status\Helpers
		lineageTag: 42e2cdf0-cab0-4e93-b370-3248bb8410c8

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Is Active Customer' = ```
			
			VAR MonthsInactive =
			    [Months Since Last Billing]
			RETURN
			IF (
			    NOT ISBLANK ( MonthsInactive )
			        && MonthsInactive < 12,
			    1,
			    0
			)
			
			```
		displayFolder: 08 Customer Status
		lineageTag: df4ebd35-6b6b-4d14-b986-0d4ec55cdb47

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Customer Lifecycle Status' = ```
			
			VAR MonthsInactive =
			    [Months Since Last Billing]
			RETURN
			SWITCH (
			    TRUE (),
			    ISBLANK ( MonthsInactive ),        "No Activity",
			    MonthsInactive < 6,                "Active",
			    MonthsInactive < 12,               "Dormant (6–11 mo)",
			    "Inactive (12+ mo)"
			)
			
			```
		displayFolder: 08 Customer Status\Helpers
		lineageTag: 2f7c11b4-4872-48c1-8d28-550e3c5a2d4c

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Months Since Last Billing' = ```
			
			VAR LastBillingDate =
			    [Last Billing Date (Any Stream)]
			VAR AnchorDate =
			    EOMONTH ( TODAY(), 0 )
			RETURN
			IF (
			    ISBLANK ( LastBillingDate ),
			    BLANK (),
			    DATEDIFF (
			        EOMONTH ( LastBillingDate, 0 ),
			        AnchorDate,
			        MONTH
			    )
			)
			
			```
		displayFolder: 08 Customer Status\Helpers
		lineageTag: 2ad2a8aa-e56a-47f2-939b-cdd871a9a2b6

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Project Revenue (TTM)' = ```
			
			VAR AnchorDate =
			    EOMONTH ( TODAY(), 0 )
			RETURN
			CALCULATE (
			    [Project Revenue],
			    DATESINPERIOD (
			        FYCALENDAR_D[Date],
			        AnchorDate,
			        -12,
			        MONTH
			    )
			)
			
			```
		displayFolder: 90 Debug\Legacy\TTM
		lineageTag: 2d6e84f6-b736-481c-a59d-c374e7256d8a

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'T&M Revenue (TTM)' = ```
			
			VAR AnchorDate =
			    EOMONTH ( TODAY(), 0 )
			RETURN
			CALCULATE (
			    [T&M Revenue],
			    DATESINPERIOD (
			        FYCALENDAR_D[Date],
			        AnchorDate,
			        -12,
			        MONTH
			    )
			)
			
			```
		displayFolder: 90 Debug\Legacy\TTM
		lineageTag: 8ffa65a2-4413-4a05-98b4-f8d31271f860

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Project Spend per Maintenance $ (TTM)' = ```
			
			VAR MaintRev =
			    [Service Contract Revenue (TTM)]
			VAR ProjectRev =
			    [Project Revenue (TTM)]
			RETURN
			IF (
			    MaintRev > 0,
			    DIVIDE ( ProjectRev, MaintRev ),
			    BLANK ()
			)
			
			```
		displayFolder: 90 Debug\Legacy\TTM
		lineageTag: d7c0537b-bd17-4831-a0a3-a53ed8f89161

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'T&M Spend per Maintenance $ (TTM)' = ```
			
			VAR MaintRev =
			    [Service Contract Revenue (TTM)]
			VAR TMRev =
			    [T&M Revenue (TTM)]
			RETURN
			IF (
			    MaintRev > 0,
			    DIVIDE ( TMRev, MaintRev ),
			    BLANK ()
			)
			
			```
		displayFolder: 90 Debug\Legacy\TTM
		lineageTag: ec03a1c3-2a27-4805-830b-2400a5b27e68

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Total T&M Spend per Total Maintenance $ (Snapshot)' = ```
			
			VAR MaintRev =
			    [Service Contract Revenue]
			VAR TMRev =
			    [T&M Revenue]
			RETURN
			IF (
			    MaintRev > 0,
			    DIVIDE ( TMRev, MaintRev ),
			    BLANK ()
			)
			
			```
		displayFolder: 09 Relationship Intensity\Snapshot
		lineageTag: 58993b39-fe01-4d1c-adca-e13a8f34d777

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Total Project Spend per Total Maintenance $ (Snapshot)' = ```
			
			VAR MaintRev =
			    [Service Contract Revenue]
			VAR ProjectRev =
			    [Project Revenue]
			RETURN
			IF (
			    MaintRev > 0,
			    DIVIDE ( ProjectRev, MaintRev ),
			    BLANK ()
			)
			
			```
		displayFolder: 09 Relationship Intensity\Snapshot
		lineageTag: a7118c0b-4cf0-4735-b0b6-5f14e88ea7a1

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Last SC Data Month End' = ```
			
			CALCULATE (
			    MAX ( SC_CustomerMonth_Revenue[Month End] ),
			    REMOVEFILTERS ( FYCALENDAR_D[Date] )
			)
			
			```
		lineageTag: a493b9c8-29f8-464d-91a4-d777a126c652

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Rolling 12-month (R12M) retention framework (As-Of, cohort-based).
	/// Uses SC_AgreementMonth_Revenue to ensure retention respects Service Contract slicers (Division, Contract Type, etc.).
	measure 'Last SC Data Month End (In Slice)' = ```
			
			  CALCULATE (
			      MAX ( SC_AgreementMonth_Revenue[Month End] ),
			      REMOVEFILTERS ( FYCALENDAR_D[Date] ),
			      REMOVEFILTERS ( CUSTOMERS_D )
			  )
			```
		displayFolder: 05 Time Intelligence – R12M\Helpers
		lineageTag: 44b3ed2e-6c08-4d4d-9a08-53f02a90c0dd

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'As Of Month End (SC)' = ```
			
			  VAR SelectedMonthEnd =
			      EOMONTH ( CALCULATE ( MAX ( FYCALENDAR_D[Date] ), REMOVEFILTERS ( CUSTOMERS_D ) ), 0 )
			  VAR LastDataMonthEnd =
			      [Last SC Data Month End (In Slice)]
			  RETURN
			      IF (
			          ISBLANK ( LastDataMonthEnd ),
			          BLANK (),
			          MIN ( SelectedMonthEnd, LastDataMonthEnd )
			      )
			```
		displayFolder: 05 Time Intelligence – R12M\Helpers
		lineageTag: 7f66c48d-4a9f-4aa9-9c86-f2d3c5f2c8e7

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'SC Revenue (Agreement Month)' = CALCULATE( SUM ( SC_AgreementMonth_Revenue[SC Revenue] ), 'FYCALENDAR_D'[Is Completed Month] = TRUE )
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 05 Time Intelligence – R12M\Helpers
		lineageTag: 63d82a1c-bd3f-4f8d-a16b-2f95578fa5a6

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'SC Revenue (R12M As Of)' = ```
			
			VAR AnchorDate =
			    [As Of Month End (SC)]
			RETURN
			    CALCULATE (
			        [SC Revenue (Agreement Month)],
			        DATESINPERIOD ( FYCALENDAR_D[Date], AnchorDate, -12, MONTH )
			    )
			
			```
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 05 Time Intelligence – R12M\Revenue
		lineageTag: 3b90e0da-2b1d-4ab2-8f0f-0b85f0ed3b8c

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'SC Revenue PY (R12M As Of)' = ```
			
			VAR AnchorDate =
			    [As Of Month End (SC)]
			VAR AnchorPY =
			    EDATE ( AnchorDate, -12 )
			RETURN
			    IF (
			        ISBLANK ( AnchorDate ),
			        BLANK (),
			        CALCULATE (
			            [SC Revenue (Agreement Month)],
			            DATESINPERIOD ( FYCALENDAR_D[Date], AnchorPY, -12, MONTH )
			        )
			    )
			
			```
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 05 Time Intelligence – R12M\Revenue
		lineageTag: 6fd2e4cc-9b07-46f5-bc16-53c2c4dd381a

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'Retention Base PY (R12M As Of)' = ```
			
			SUMX (
			    VALUES ( CUSTOMERS_D[Customer Key] ),
			    VAR RevPY = [SC Revenue PY (R12M As Of)]
			    RETURN IF ( RevPY > 0, RevPY, 0 )
			)
			
			```
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 05 Time Intelligence – R12M\Components
		lineageTag: 84aebcb0-65bb-4871-b62b-4d56c2c7b4b6

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'Churn Revenue (R12M As Of)' = ```
			
			SUMX (
			    VALUES ( CUSTOMERS_D[Customer Key] ),
			    VAR RevCY = [SC Revenue (R12M As Of)]
			    VAR RevPY = [SC Revenue PY (R12M As Of)]
			    RETURN
			        IF ( RevPY > 0 && ( ISBLANK ( RevCY ) || RevCY = 0 ), RevPY, 0 )
			)
			
			```
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 05 Time Intelligence – R12M\Components
		lineageTag: a1e3513e-3fb5-4d05-98c7-7ce1c76ed8ef

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'Contraction Revenue (R12M As Of)' = ```
			
			SUMX (
			    VALUES ( CUSTOMERS_D[Customer Key] ),
			    VAR RevCY = [SC Revenue (R12M As Of)]
			    VAR RevPY = [SC Revenue PY (R12M As Of)]
			    RETURN
			        IF ( RevPY > 0 && RevCY > 0 && RevCY < RevPY, RevPY - RevCY, 0 )
			)
			
			```
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 05 Time Intelligence – R12M\Components
		lineageTag: 2c2a9a3d-6b39-4c6e-89ef-70f22b3b79e4

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'Upsell Revenue (R12M As Of)' = ```
			
			SUMX (
			    VALUES ( CUSTOMERS_D[Customer Key] ),
			    VAR RevCY = [SC Revenue (R12M As Of)]
			    VAR RevPY = [SC Revenue PY (R12M As Of)]
			    RETURN
			        IF ( RevPY > 0 && RevCY > RevPY, RevCY - RevPY, 0 )
			)
			
			```
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 05 Time Intelligence – R12M\Components
		lineageTag: 5d0e8e0a-bfe0-49fb-ae20-3b3c332b31da

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'Net Revenue Retention % (R12M As Of)' = ```
			
			DIVIDE (
			    [Retention Base PY (R12M As Of)]
			        - [Churn Revenue (R12M As Of)]
			        - [Contraction Revenue (R12M As Of)]
			        + [Upsell Revenue (R12M As Of)],
			    [Retention Base PY (R12M As Of)]
			)
			
			```
		formatString: 0.0%;-0.0%;0.0%
		displayFolder: 07 Retention KPIs – R12M As Of\NRR
		lineageTag: e635ff5f-45e8-4cfd-a0a9-f15fd7e79595

	measure 'Gross Revenue Retention % (R12M As Of)' = ```
			
			DIVIDE (
			    [Retention Base PY (R12M As Of)]
			        - [Churn Revenue (R12M As Of)]
			        - [Contraction Revenue (R12M As Of)],
			    [Retention Base PY (R12M As Of)]
			)
			
			```
		formatString: 0.0%;-0.0%;0.0%
		displayFolder: 07 Retention KPIs – R12M As Of\GRR
		lineageTag: 7d1c6be3-5f3c-4a67-9f93-5cf2d047b1c9

	measure 'Project Revenue (R12M As Of)' =
			VAR AnchorDate =
			    [As Of Month End (Financials)]
			RETURN
			    IF (
			        ISBLANK ( AnchorDate ),
			        BLANK (),
			        CALCULATE (
			            [Project Revenue],
			            DATESINPERIOD ( FYCALENDAR_D[Date], AnchorDate, -12, MONTH )
			        )
			    )
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 05 Time Intelligence – R12M\Revenue
		lineageTag: 1b46acdb-00e2-4c7a-81a3-5cd5d692a9a8

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'T&M Revenue (R12M As Of)' =
			VAR AnchorDate =
			    [As Of Month End (Financials)]
			RETURN
			    IF (
			        ISBLANK ( AnchorDate ),
			        BLANK (),
			        CALCULATE (
			            [T&M Revenue],
			            DATESINPERIOD ( FYCALENDAR_D[Date], AnchorDate, -12, MONTH )
			        )
			    )
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 05 Time Intelligence – R12M\Revenue
		lineageTag: 8b5d1f58-0b38-4b75-9a2c-d7e3cba4d1b9

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'Project Spend per Maintenance $ (R12M As Of)' = ```
			
			VAR MaintRev =
			    [SC Revenue (R12M As Of)]
			VAR ProjectRev =
			    [Project Revenue (R12M As Of)]
			RETURN
			    IF ( MaintRev > 0, DIVIDE ( ProjectRev, MaintRev ), BLANK () )
			
			```
		displayFolder: 09 Relationship Intensity\R12M As Of
		lineageTag: 3ad6b3a2-4b7e-4c3f-9a5b-9d987b2c0b05

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'T&M Spend per Maintenance $ (R12M As Of)' = ```
			
			VAR MaintRev =
			    [SC Revenue (R12M As Of)]
			VAR TMRev =
			    [T&M Revenue (R12M As Of)]
			RETURN
			    IF ( MaintRev > 0, DIVIDE ( TMRev, MaintRev ), BLANK () )
			
			```
		displayFolder: 09 Relationship Intensity\R12M As Of
		lineageTag: 75c7c7fd-9a51-48ff-b9d0-8d5d85155ed7

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure SelectedFiscalYearWithLatestDate = ```
			
			VAR SelYears =
			    VALUES(FYCALENDAR_D[Fiscal Year])
			VAR YearText =
			    IF(
			        ISFILTERED(FYCALENDAR_D[Fiscal Year]),
			        CONCATENATEX(
			            SelYears,
			            FYCALENDAR_D[Fiscal Year],
			            ", ",
			            FYCALENDAR_D[Fiscal Year],
			            ASC
			        ),
			        "All Fiscal Years"
			    )
			VAR LatestDateFiltered =
			    CALCULATE(
			        MAX(FYCALENDAR_D[Date]),   -- Change column name if your date field is different
			        ALLSELECTED(FYCALENDAR_D)  -- Respects current filters from slicers/visuals
			    )
			RETURN
			YearText &
			" | Latest Date: " &
			IF(
			    NOT ISBLANK(LatestDateFiltered),
			    FORMAT(LatestDateFiltered, "dd-MMM-yyyy"),
			    "No Data"
			)
			
			```
		displayFolder: 90 Debug\UI
		lineageTag: 2bfefab8-9456-48f6-abc2-3a97f85aac78

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'First Contract Billing Date' = ```
			
			MIN ( SERVCONTRACTS_REVENUE_F[Date] )
			
			```
		displayFolder: 08 Customer Status\Contract
		lineageTag: c48c1848-76b5-4777-bb45-3fd82c3fa9a6

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Last Contract Billing Date' = ```
			
			MAX ( SERVCONTRACTS_REVENUE_F[Date] )
			
			```
		displayFolder: 08 Customer Status\Contract
		lineageTag: b94eafa1-d297-4978-a13d-657987289e75

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Contract Billing Period (Months Inclusive)' = ```
			
			VAR FirstDt = [First Contract Billing Date]
			VAR LastDt  = [Last Contract Billing Date]
			RETURN
			IF (
			    ISBLANK ( FirstDt ) || ISBLANK ( LastDt ),
			    BLANK (),
			    DATEDIFF ( EOMONTH ( FirstDt, -1 ) + 1, EOMONTH ( LastDt, 0 ) + 1, MONTH )
			)
			
			```
		displayFolder: 08 Customer Status\Contract
		lineageTag: 5f675a28-78d7-4a06-9b11-7001a878fc5d

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Active Contracts' =
			
			CALCULATE (
			    COUNTROWS ( 'SERVCONTRACTS_D' ),
			    'SERVCONTRACTS_D'[IsActiveContractYear] = TRUE ()
			)
		displayFolder: 08 Customer Status\Contract
		lineageTag: a68c3c98-26e5-4b8d-9aec-67eaa1e40ca7

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Active Projects' = ```
			
			CALCULATE (
			    COUNTROWS ( PROJECTS_D ),
			    PROJECTS_D[Inactive] = FALSE ()
			)
			
			```
		displayFolder: 08 Customer Status
		lineageTag: 19fc2dfb-121d-4317-a2ec-b425f9227581

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Client Revenue Rank' = ```
			
			RANKX(
			    ALLSELECTED( CUSTOMERS_D[Customer Name] ),
			    [Total Revenue],
			    ,
			    DESC,
			    Dense
			)
			
			```
		lineageTag: c1a146cf-4865-41b0-aa71-d9431406045c

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Is In Top N Customers' = ```
			
			VAR N = SELECTEDVALUE ( 'Top N'[Top N], 10 )
			RETURN
			IF ( [Client Revenue Rank] <= N, 1, 0 )
			
			```
		lineageTag: db008e22-2cf2-4701-a44b-738372b571c6

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Debug – Calendar Min Date' =
			
			MIN ( FYCALENDAR_D[Date] )
		displayFolder: 90 Debug
		lineageTag: b33e0703-11e8-458e-92b1-da25f8c54052

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Debug – Calendar Max Date' =
			
			MAX ( FYCALENDAR_D[Date] )
		displayFolder: 90 Debug
		lineageTag: a84643e0-4656-4021-9366-1429e7547422

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Selected Fiscal Year (Single)' =
			
			SWITCH (
			    TRUE (),
			    HASONEVALUE ( FYCALENDAR_D[Fiscal Year] ),
			        SELECTEDVALUE ( FYCALENDAR_D[Fiscal Year] ),
			    ISFILTERED ( FYCALENDAR_D[Fiscal Year] ),
			        BLANK (),
			    CALCULATE (
			        MAX ( FYCALENDAR_D[Fiscal Year] ),
			        REMOVEFILTERS ( FYCALENDAR_D ),
			        FYCALENDAR_D[Is Current Fiscal Year] = TRUE ()
			    )
			)
		displayFolder: 04 Time Intelligence – Fiscal\Helpers
		lineageTag: 25d0f64f-0cf6-4ff1-8642-2b2fdbcde556

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Selected FY Start Date' = ```
			
			VAR FY =
			    [Selected Fiscal Year (Single)]
			RETURN
			    IF (
			        ISBLANK ( FY ),
			        BLANK (),
			        CALCULATE (
			            MIN ( FYCALENDAR_D[Date] ),
			            REMOVEFILTERS ( FYCALENDAR_D ),
			            FYCALENDAR_D[Fiscal Year] = FY
			        )
			    )
			
			```
		displayFolder: 04 Time Intelligence – Fiscal\Helpers
		lineageTag: f88a4eab-e68f-4ea9-b2ed-1aff3f5ea074

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Selected FY End Date' = ```
			
			VAR FY =
			    [Selected Fiscal Year (Single)]
			RETURN
			    IF (
			        ISBLANK ( FY ),
			        BLANK (),
			        CALCULATE (
			            MAX ( FYCALENDAR_D[Date] ),
			            REMOVEFILTERS ( FYCALENDAR_D ),
			            FYCALENDAR_D[Fiscal Year] = FY
			        )
			    )
			
			```
		displayFolder: 04 Time Intelligence – Fiscal\Helpers
		lineageTag: 0f41be7a-2ac9-439c-a2f6-34071aa33780

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Selected FY Is Current' = ```
			
			VAR FY =
			    [Selected Fiscal Year (Single)]
			VAR Offset =
			    CALCULATE (
			        MAX ( FYCALENDAR_D[Fiscal Year Offset] ),
			        REMOVEFILTERS ( FYCALENDAR_D ),
			        FYCALENDAR_D[Fiscal Year] = FY
			    )
			RETURN
			    IF ( ISBLANK ( FY ), BLANK (), IF ( Offset = 0, 1, 0 ) )
			
			```
		displayFolder: 04 Time Intelligence – Fiscal\Helpers
		lineageTag: 0111157d-02c0-4f18-abed-ad7ab22c1b6e

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Last Revenue Date (Selected FY)' = ```
			
			VAR StartDate =
			    [Selected FY Start Date]
			VAR EndDate =
			    [Selected FY End Date]
			RETURN
			    IF (
			        ISBLANK ( StartDate ) || ISBLANK ( EndDate ),
			        BLANK (),
			        VAR DateRange =
			            DATESBETWEEN ( FYCALENDAR_D[Date], StartDate, EndDate )
			        VAR CompletedMonthFilter = 
			            FILTER( ALL( FYCALENDAR_D ), FYCALENDAR_D[Is Completed Month] = TRUE() )
			        VAR ProjectDate =
			            CALCULATE (
			                MAX ( PROJECT_REVENUE_F[Date] ),
			                REMOVEFILTERS ( FYCALENDAR_D ),
			                DateRange,
			                CompletedMonthFilter,
			                REMOVEFILTERS ( CUSTOMERS_D )
			            )
			        VAR ServiceContractDate =
			            CALCULATE (
			                MAX ( SERVCONTRACTS_REVENUE_F[Date] ),
			                REMOVEFILTERS ( FYCALENDAR_D ),
			                DateRange,
			                CompletedMonthFilter,
			                REMOVEFILTERS ( CUSTOMERS_D )
			            )
			        VAR TMDate =
			            CALCULATE (
			                MAX ( SPOT_FINANCIALS_F[Date] ),
			                REMOVEFILTERS ( FYCALENDAR_D ),
			                DateRange,
			                CompletedMonthFilter,
			                REMOVEFILTERS ( CUSTOMERS_D )
			            )
			        RETURN
			        MAXX (
			            UNION (
			                ROW ( "d", ProjectDate ),
			                ROW ( "d", ServiceContractDate ),
			                ROW ( "d", TMDate )
			            ),
			            [d]
			        )
			    )
			
			```
		displayFolder: 04 Time Intelligence – Fiscal\Helpers
		lineageTag: 7fdfead3-1ce0-45bc-b2e8-63a61af89f70

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Last Cost Date (Selected FY)' = ```
			
			VAR StartDate =
			    [Selected FY Start Date]
			VAR EndDate =
			    [Selected FY End Date]
			RETURN
			    IF (
			        ISBLANK ( StartDate ) || ISBLANK ( EndDate ),
			        BLANK (),
			        VAR DateRange =
			            DATESBETWEEN ( FYCALENDAR_D[Date], StartDate, EndDate )
			        VAR CompletedMonthFilter = 
			            FILTER( ALL( FYCALENDAR_D ), FYCALENDAR_D[Is Completed Month] = TRUE() )
			        VAR ProjectDate =
			            CALCULATE (
			                MAX ( PROJECT_COSTS_F[Date] ),
			                REMOVEFILTERS ( FYCALENDAR_D ),
			                DateRange,
			                CompletedMonthFilter,
			                REMOVEFILTERS ( CUSTOMERS_D )
			            )
			        VAR ServiceContractDate =
			            CALCULATE (
			                MAX ( SERVCONTRACTS_COSTS_F[Date] ),
			                REMOVEFILTERS ( FYCALENDAR_D ),
			                DateRange,
			                CompletedMonthFilter,
			                REMOVEFILTERS ( CUSTOMERS_D )
			            )
			        VAR TMDate =
			            CALCULATE (
			                MAX ( SPOT_FINANCIALS_F[Date] ),
			                REMOVEFILTERS ( FYCALENDAR_D ),
			                DateRange,
			                CompletedMonthFilter,
			                REMOVEFILTERS ( CUSTOMERS_D )
			            )
			        RETURN
			        MAXX (
			            UNION (
			                ROW ( "d", ProjectDate ),
			                ROW ( "d", ServiceContractDate ),
			                ROW ( "d", TMDate )
			            ),
			            [d]
			        )
			    )
			
			```
		displayFolder: 04 Time Intelligence – Fiscal\Helpers
		lineageTag: 7669bfb2-42a3-49ca-ad7f-ecc9190d6062

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'As Of Date (Revenue)' = ```
			
			VAR IsCurrentFY =
			    [Selected FY Is Current]
			VAR EndDate =
			    [Selected FY End Date]
			VAR LastDataDate =
			    [Last Revenue Date (Selected FY)]
			RETURN
			    IF ( IsCurrentFY = 1, LastDataDate, EndDate )
			
			```
		displayFolder: 04 Time Intelligence – Fiscal\Helpers
		lineageTag: 03bf7bc1-8dd1-45d7-9dd0-514b6f5c08d1

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'As Of Date (Cost)' = ```
			
			VAR IsCurrentFY =
			    [Selected FY Is Current]
			VAR EndDate =
			    [Selected FY End Date]
			VAR LastDataDate =
			    [Last Cost Date (Selected FY)]
			RETURN
			    IF ( IsCurrentFY = 1, LastDataDate, EndDate )
			
			```
		displayFolder: 04 Time Intelligence – Fiscal\Helpers
		lineageTag: 9ae25c39-ceb5-4725-8dbc-606e388c86cc

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'As Of Date (Financials)' = ```
			
			VAR IsCurrentFY =
			    [Selected FY Is Current]
			VAR EndDate =
			    [Selected FY End Date]
			VAR RevDate =
			    [As Of Date (Revenue)]
			VAR CostDate =
			    [As Of Date (Cost)]
			VAR MinDate =
			    IF (
			        ISBLANK ( RevDate ),
			        CostDate,
			        IF ( ISBLANK ( CostDate ), RevDate, MIN ( RevDate, CostDate ) )
			    )
			RETURN
			    IF ( IsCurrentFY = 1, MinDate, EndDate )
			
			```
		displayFolder: 04 Time Intelligence – Fiscal\Helpers
		lineageTag: e56d7e3f-5cfc-46c7-bd8c-29931722fc62

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Annualized YTD Comparison. Forces logic to 'Start of Year -> As Of Date'. Use ONLY for Annual Tables or KPI Scorecards. Do not use for Quarters/Months.
	measure 'Total Revenue (FY As Of)' = ```
			
			VAR StartDate =
			    [Selected FY Start Date]
			VAR EndDate =
			    [As Of Date (Revenue)]
			RETURN
			    IF (
			        ISBLANK ( StartDate ) || ISBLANK ( EndDate ),
			        BLANK (),
			        CALCULATE (
			            [Total Revenue],
			            REMOVEFILTERS ( FYCALENDAR_D ),
			            DATESBETWEEN ( FYCALENDAR_D[Date], StartDate, EndDate )
			        )
			    )
			
			```
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 04 Time Intelligence – Fiscal\Revenue
		lineageTag: f912db72-60c0-4102-b6a0-155bfc650920

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'Total Revenue PY (FY As Of)' = ```
			
			VAR StartDate =
			    [Selected FY Start Date]
			VAR EndDate =
			    [As Of Date (Revenue)]
			VAR StartDatePY =
			    EDATE ( StartDate, -12 )
			VAR EndDatePY =
			    EDATE ( EndDate, -12 )
			RETURN
			    IF (
			        ISBLANK ( StartDate ) || ISBLANK ( EndDate ),
			        BLANK (),
			        CALCULATE (
			            [Total Revenue],
			            REMOVEFILTERS ( FYCALENDAR_D ),
			            DATESBETWEEN ( FYCALENDAR_D[Date], StartDatePY, EndDatePY )
			        )
			    )
			
			```
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 04 Time Intelligence – Fiscal\Revenue
		lineageTag: 5003f6df-75b3-44d1-a25f-c98f5ea1725e

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'Total Revenue YoY Δ (FY As Of)' = [Total Revenue (FY As Of)] - [Total Revenue PY (FY As Of)]
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 04 Time Intelligence – Fiscal\Revenue
		lineageTag: 6e49b1bc-525f-4fc0-b10d-0858a564232e

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'Total Revenue YoY % (FY As Of)' = DIVIDE([Total Revenue YoY Δ (FY As Of)], [Total Revenue PY (FY As Of)])
		formatString: 0.0%;-0.0%;0.0%
		displayFolder: 04 Time Intelligence – Fiscal\Revenue
		lineageTag: 39790dd6-6812-4abf-8244-c83472102f71

	/// Annualized YTD Comparison. Forces logic to 'Start of Year -> As Of Date'. Use ONLY for Annual Tables or KPI Scorecards. Do not use for Quarters/Months.
	measure 'Total Gross Profit (FY As Of)' = ```
			
			VAR StartDate =
			    [Selected FY Start Date]
			VAR EndDate =
			    [As Of Date (Financials)]
			RETURN
			    IF (
			        ISBLANK ( StartDate ) || ISBLANK ( EndDate ),
			        BLANK (),
			        CALCULATE (
			            [Total Gross Profit],
			            REMOVEFILTERS ( FYCALENDAR_D ),
			            DATESBETWEEN ( FYCALENDAR_D[Date], StartDate, EndDate )
			        )
			    )
			
			```
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 04 Time Intelligence – Fiscal\Gross Profit
		lineageTag: 85a9f2a8-149c-41b0-9087-43ef5cc034dc

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'Total Gross Profit PY (FY As Of)' = ```
			
			VAR StartDate =
			    [Selected FY Start Date]
			VAR EndDate =
			    [As Of Date (Financials)]
			VAR StartDatePY =
			    EDATE ( StartDate, -12 )
			VAR EndDatePY =
			    EDATE ( EndDate, -12 )
			RETURN
			    IF (
			        ISBLANK ( StartDate ) || ISBLANK ( EndDate ),
			        BLANK (),
			        CALCULATE (
			            [Total Gross Profit],
			            REMOVEFILTERS ( FYCALENDAR_D ),
			            DATESBETWEEN ( FYCALENDAR_D[Date], StartDatePY, EndDatePY )
			        )
			    )
			
			```
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 04 Time Intelligence – Fiscal\Gross Profit
		lineageTag: b7675a05-d8d8-4a58-890f-3263c4f18632

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'Total Gross Profit YoY Δ (FY As Of)' = [Total Gross Profit (FY As Of)] - [Total Gross Profit PY (FY As Of)]
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 04 Time Intelligence – Fiscal\Gross Profit
		lineageTag: 4192b195-bc29-40f9-83f8-0fbd586c4f19

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'Total Gross Profit YoY % (FY As Of)' =
			DIVIDE(
						[Total Gross Profit YoY Δ (FY As Of)],
						[Total Gross Profit PY (FY As Of)]
					)
		formatString: 0.0%;-0.0%;0.0%
		displayFolder: 04 Time Intelligence – Fiscal\Gross Profit
		lineageTag: 189c18f0-a543-4323-97b2-16f3177275fa

	/// Annualized YTD Comparison. Forces logic to 'Start of Year -> As Of Date'. Use ONLY for Annual Tables or KPI Scorecards. Do not use for Quarters/Months.
	measure 'Total GP % (FY As Of)' =
			DIVIDE(
						[Total Gross Profit (FY As Of)],
						[Total Revenue (FY As Of)]
					)
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: 04 Time Intelligence – Fiscal\Gross Profit
		lineageTag: 4d1f665d-d2d5-4496-ab39-66e399c7b769

	measure 'Total GP % PY (FY As Of)' =
			DIVIDE(
						[Total Gross Profit PY (FY As Of)],
						[Total Revenue PY (FY As Of)]
					)
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: 04 Time Intelligence – Fiscal\Gross Profit
		lineageTag: 9e272676-cbb7-4727-8b89-7ae62a2be3f6

	measure 'Total GP % YoY Δ (pp) (FY As Of)' = [Total GP % (FY As Of)] - [Total GP % PY (FY As Of)]
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: 04 Time Intelligence – Fiscal\Gross Profit
		lineageTag: 20e66d07-7b29-45b6-b1d7-434c8ec5d0cd

	/// Standard Period Comparison. Compares the selected period (Month/Quarter) to the same period last year. Use for Charts and Trend Tables.
	measure 'Total Revenue Trend vs PY' = ```
			
			VAR CY = [Revenue (By Stream)]
			VAR PY = [Total Revenue PY]
			RETURN
			IF( 
			    ISBLANK(CY) || CY = 0, 
			    BLANK(),
			    VAR Diff = CY - PY
			    VAR PctChange = DIVIDE( Diff, PY )
			    VAR Icon = 
			        SWITCH( TRUE(),
			            PctChange > 0, UNICHAR(9650), -- ▲
			            PctChange < 0, UNICHAR(9660), -- ▼
			            UNICHAR(9644)                 -- ▬
			        )
			    VAR PctText = 
			        IF( PctChange >= 0, 
			            UNICHAR(8199) & FORMAT( PctChange, "0.0%" ), 
			            FORMAT( PctChange, "0.0%" )
			        )
			    RETURN
			        Icon & " " & PctText
			)
			
			```
		displayFolder: 01 Revenue
		lineageTag: 8df6e039-72a0-4bff-b04f-22fc4ce7f34f

	measure 'Total Gross Profit PY' = ```
			
			CALCULATE(
			    [Gross Profit (By Stream)],
			    SAMEPERIODLASTYEAR( 'FYCALENDAR_D'[Date] )
			)
			
			```
		displayFolder: 03 Gross Profit\Total
		lineageTag: 845b89e3-0e5f-4f63-a474-76604f8e4b35

	/// Standard Period Comparison. Compares the selected period (Month/Quarter) to the same period last year. Use for Charts and Trend Tables.
	measure 'Total Gross Profit Trend vs PY' = ```
			
			VAR CY = [Gross Profit (By Stream)]
			VAR PY = [Total Gross Profit PY]
			RETURN
			IF( 
			    ISBLANK(CY) || CY = 0, 
			    BLANK(),
			    VAR Diff = CY - PY
			    VAR PctChange = DIVIDE( Diff, PY )
			    VAR Icon = 
			        SWITCH( TRUE(),
			            PctChange > 0, UNICHAR(9650), -- ▲
			            PctChange < 0, UNICHAR(9660), -- ▼
			            UNICHAR(9644)                 -- ▬
			        )
			    VAR PctText = 
			        IF( PctChange >= 0, 
			            UNICHAR(8199) & FORMAT( PctChange, "0.0%" ), 
			            FORMAT( PctChange, "0.0%" )
			        )
			    RETURN
			        Icon & " " & PctText
			)
			
			```
		displayFolder: 03 Gross Profit\Total
		lineageTag: c43433be-60f2-4c3a-af96-72b98840f567

	measure 'Retention Base Count PY (SC) (R12M As Of)' = ```
			
			COUNTROWS (
			    FILTER (
			        VALUES ( CUSTOMERS_D[Customer Key] ),
			        [SC Revenue PY (R12M As Of)] > 0
			    )
			)
			
			```
		displayFolder: 07 Retention KPIs – R12M As Of\Logo Retention (SC)
		lineageTag: d4431787-0b1a-4f43-99c1-ff4968445c3a

	measure 'Logo Churn Count (SC) (R12M As Of)' = ```
			
			COUNTROWS (
			    FILTER (
			        VALUES ( CUSTOMERS_D[Customer Key] ),
			        [SC Revenue PY (R12M As Of)] > 0
			            && ( ISBLANK ( [SC Revenue (R12M As Of)] )
			            || [SC Revenue (R12M As Of)] = 0 )
			    )
			)
			
			```
		displayFolder: 07 Retention KPIs – R12M As Of\Logo Retention (SC)
		lineageTag: b88cefa2-4b4a-433d-813d-81dc2cfe07be

	measure 'Logo Retention % (SC) (R12M As Of)' = ```
			
			VAR Base = [Retention Base Count PY (SC) (R12M As Of)]
			VAR Churn = [Logo Churn Count (SC) (R12M As Of)]
			RETURN
			    IF ( Base > 0, DIVIDE ( Base - Churn, Base ) )
			
			```
		formatString: 0.0%;-0.0%;0.0%
		displayFolder: 07 Retention KPIs – R12M As Of\Logo Retention (SC)
		lineageTag: d3969edf-4fd4-4d2c-a3fa-a1363f9a36dc

	measure 'Total Revenue (R12M As Of)' =
			VAR AnchorDate = [As Of Month End (Financials)]
			RETURN
			    IF (
			        ISBLANK ( AnchorDate ),
			        BLANK (),
			        CALCULATE (
			            [Total Revenue],
			            DATESINPERIOD ( FYCALENDAR_D[Date], AnchorDate, -12, MONTH )
			        )
			    )
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 05 Time Intelligence – R12M\Revenue
		lineageTag: 84cdb3d9-c973-4eb7-9946-3c92a41b579a

	measure 'Total Revenue PY (R12M As Of)' =
			VAR AnchorDate = [As Of Month End (Financials)]
			VAR AnchorPY = EDATE ( AnchorDate, -12 )
			RETURN
			    IF (
			        ISBLANK ( AnchorDate ),
			        BLANK (),
			        CALCULATE (
			            [Total Revenue],
			            DATESINPERIOD ( FYCALENDAR_D[Date], AnchorPY, -12, MONTH )
			        )
			    )
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 05 Time Intelligence – R12M\Revenue
		lineageTag: 78c129e2-2d46-4696-a0fc-5cb8060465a8

	measure 'Retention Base Count PY (Total) (R12M As Of)' = ```
			
			COUNTROWS (
			    FILTER (
			        VALUES ( CUSTOMERS_D[Customer Key] ),
			        [Total Revenue PY (R12M As Of)] > 0
			    )
			)
			
			```
		displayFolder: 07 Retention KPIs – R12M As Of\Logo Retention (Total)
		lineageTag: 07df03f6-5c9a-4c63-8311-1f697396ebec

	measure 'Logo Churn Count (Total) (R12M As Of)' = ```
			
			COUNTROWS (
			    FILTER (
			        VALUES ( CUSTOMERS_D[Customer Key] ),
			        [Total Revenue PY (R12M As Of)] > 0
			            && ( ISBLANK ( [Total Revenue (R12M As Of)] )
			            || [Total Revenue (R12M As Of)] = 0 )
			    )
			)
			
			```
		displayFolder: 07 Retention KPIs – R12M As Of\Logo Retention (Total)
		lineageTag: 62ba3163-f056-447e-8712-d2b7716d084f

	measure 'Logo Retention % (Total) (R12M As Of)' = ```
			
			VAR Base = [Retention Base Count PY (Total) (R12M As Of)]
			VAR Churn = [Logo Churn Count (Total) (R12M As Of)]
			RETURN
			    IF ( Base > 0, DIVIDE ( Base - Churn, Base ) )
			
			```
		formatString: 0.0%;-0.0%;0.0%
		displayFolder: 07 Retention KPIs – R12M As Of\Logo Retention (Total)
		lineageTag: bca4ab17-131c-4da8-9b21-29cba7da343a

	measure 'Net Revenue Retention % PY (R12M As Of)' = ```
			
			VAR CurrentAnchor = [As Of Month End (SC)]
			VAR PriorAnchor = EDATE( CurrentAnchor, -12 )
			RETURN
			    CALCULATE(
			        [Net Revenue Retention % (R12M As Of)],
			        'FYCALENDAR_D'[Date] = PriorAnchor
			    )
			
			```
		formatString: 0.0%;-0.0%;0.0%
		displayFolder: 07 Retention KPIs – R12M As Of\NRR
		lineageTag: 5be67caa-d585-4f26-ac2b-6817d043c880

	measure 'Gross Revenue Retention % PY (R12M As Of)' = ```
			
			VAR CurrentAnchor = [As Of Month End (SC)]
			VAR PriorAnchor = EDATE( CurrentAnchor, -12 )
			RETURN
			    CALCULATE(
			        [Gross Revenue Retention % (R12M As Of)],
			        'FYCALENDAR_D'[Date] = PriorAnchor
			    )
			
			```
		formatString: 0.0%;-0.0%;0.0%
		displayFolder: 07 Retention KPIs – R12M As Of\GRR
		lineageTag: 64f790ef-c364-44d1-8717-ba78a7089425

	measure 'Logo Retention % (SC) PY (R12M As Of)' = ```
			
			VAR CurrentAnchor = [As Of Month End (SC)]
			VAR PriorAnchor = EDATE( CurrentAnchor, -12 )
			RETURN
			    CALCULATE(
			        [Logo Retention % (SC) (R12M As Of)],
			        'FYCALENDAR_D'[Date] = PriorAnchor
			    )
			
			```
		formatString: 0.0%;-0.0%;0.0%
		displayFolder: 07 Retention KPIs – R12M As Of\Logo Retention (SC)
		lineageTag: 276a3c05-d467-4248-9416-513f3984a37d

	measure 'Logo Retention % (Total) PY (R12M As Of)' = ```
			
			VAR CurrentAnchor = [As Of Month End (SC)]
			VAR PriorAnchor = EDATE( CurrentAnchor, -12 )
			RETURN
			    CALCULATE(
			        [Logo Retention % (Total) (R12M As Of)],
			        'FYCALENDAR_D'[Date] = PriorAnchor
			    )
			
			```
		formatString: 0.0%;-0.0%;0.0%
		displayFolder: 07 Retention KPIs – R12M As Of\Logo Retention (Total)
		lineageTag: b2692f34-2e7b-4eba-8a28-af80d2f582a6

	measure 'Net Revenue Retention Trend vs PY (R12M As Of)' = ```
			
			VAR CY = [Net Revenue Retention % (R12M As Of)]
			VAR PY = [Net Revenue Retention % PY (R12M As Of)]
			
			RETURN
			IF( 
			    ISBLANK(CY) || CY = 0 || ISBLANK(PY) || PY = 0, 
			    BLANK(),
			    VAR Diff = CY - PY
			    VAR Icon = 
			        SWITCH( TRUE(),
			            Diff > 0, UNICHAR(9650), -- ▲
			            Diff < 0, UNICHAR(9660), -- ▼
			            UNICHAR(9644)            -- ▬
			        )
			    VAR PctText = 
			        IF( Diff >= 0, 
			            UNICHAR(8199) & FORMAT( Diff, "0.0%" ), 
			            FORMAT( Diff, "0.0%" )
			        )
			    RETURN
			        Icon & " " & PctText
			)
			
			```
		displayFolder: 07 Retention KPIs – R12M As Of\NRR
		lineageTag: 3d0b2006-c033-4074-8077-7f9c7bebac2a

	measure 'Gross Revenue Retention Trend vs PY (R12M As Of)' = ```
			
			VAR CY = [Gross Revenue Retention % (R12M As Of)]
			VAR PY = [Gross Revenue Retention % PY (R12M As Of)]
			
			RETURN
			IF( 
			    ISBLANK(CY) || CY = 0 || ISBLANK(PY) || PY = 0, 
			    BLANK(),
			    VAR Diff = CY - PY
			    VAR Icon = 
			        SWITCH( TRUE(),
			            Diff > 0, UNICHAR(9650), -- ▲
			            Diff < 0, UNICHAR(9660), -- ▼
			            UNICHAR(9644)            -- ▬
			        )
			    VAR PctText = 
			        IF( Diff >= 0, 
			            UNICHAR(8199) & FORMAT( Diff, "0.0%" ), 
			            FORMAT( Diff, "0.0%" )
			        )
			    RETURN
			        Icon & " " & PctText
			)
			
			```
		displayFolder: 07 Retention KPIs – R12M As Of\GRR
		lineageTag: 6fd77b3d-4302-42cb-8275-75b471477685

	measure 'Logo Retention Trend (SC) vs PY (R12M As Of)' = ```
			
			VAR CY = [Logo Retention % (SC) (R12M As Of)]
			VAR PY = [Logo Retention % (SC) PY (R12M As Of)]
			
			RETURN
			IF( 
			    ISBLANK(CY) || CY = 0 || ISBLANK(PY) || PY = 0, 
			    BLANK(),
			    VAR Diff = CY - PY
			    VAR Icon = 
			        SWITCH( TRUE(),
			            Diff > 0, UNICHAR(9650), -- ▲
			            Diff < 0, UNICHAR(9660), -- ▼
			            UNICHAR(9644)            -- ▬
			        )
			    VAR PctText = 
			        IF( Diff >= 0, 
			            UNICHAR(8199) & FORMAT( Diff, "0.0%" ), 
			            FORMAT( Diff, "0.0%" )
			        )
			    RETURN
			        Icon & " " & PctText
			)
			
			```
		displayFolder: 07 Retention KPIs – R12M As Of\Logo Retention (SC)
		lineageTag: dd9cd90f-bfb8-4c7b-9439-48337aa3ed2f

	measure 'Logo Retention Trend (Total) vs PY (R12M As Of)' = ```
			
			VAR CY = [Logo Retention % (Total) (R12M As Of)]
			VAR PY = [Logo Retention % (Total) PY (R12M As Of)]
			
			RETURN
			IF( 
			    ISBLANK(CY) || CY = 0 || ISBLANK(PY) || PY = 0, 
			    BLANK(),
			    VAR Diff = CY - PY
			    VAR Icon = 
			        SWITCH( TRUE(),
			            Diff > 0, UNICHAR(9650), -- ▲
			            Diff < 0, UNICHAR(9660), -- ▼
			            UNICHAR(9644)            -- ▬
			        )
			    VAR PctText = 
			        IF( Diff >= 0, 
			            UNICHAR(8199) & FORMAT( Diff, "0.0%" ), 
			            FORMAT( Diff, "0.0%" )
			        )
			    RETURN
			        Icon & " " & PctText
			)
			
			```
		displayFolder: 07 Retention KPIs – R12M As Of\Logo Retention (Total)
		lineageTag: 096afa21-b29a-43e8-aaa8-47fc7936cdee

	measure 'Currently Active Date Range' = ```
			
			VAR MinDate = MIN( 'FYCALENDAR_D'[Date] )
			VAR MaxDate = MAX( 'FYCALENDAR_D'[Date] )
			VAR MinText = FORMAT( MinDate, "MMM yyyy" )
			VAR MaxText = FORMAT( MaxDate, "MMM yyyy" )
			RETURN
			    IF( 
			        MinText = MaxText, 
			        MinText, 
			        MinText & " – " & MaxText 
			    )
			
			```
		displayFolder: 90 Debug\UI
		lineageTag: 143fdd9d-2321-443e-9fae-221b8dad4b9b

	measure 'Revenue (By Stream) - Original' = ```
			
			VAR Stream =
			    SELECTEDVALUE ( Revenue_Stream_D[Revenue Stream] )
			RETURN
			    IF (
			        ISBLANK ( Stream ),
			        [Total Revenue],
			        SWITCH (
			            Stream,
			            "Project", [Project Revenue],
			            "Service Contract", [Service Contract Revenue],
			            "T&M", [T&M Revenue]
			        )
			    )
			
			```
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 90 Debug\Backups
		lineageTag: 11d57eed-ad4a-4ff2-bb0c-49b69124b240

	measure 'Cost (By Stream) - Original' = ```
			
			VAR Stream =
			    SELECTEDVALUE ( Revenue_Stream_D[Revenue Stream] )
			RETURN
			    IF (
			        ISBLANK ( Stream ),
			        [Total Cost],
			        SWITCH (
			            Stream,
			            "Project", [Project Cost],
			            "Service Contract", [Service Contract Cost],
			            "T&M", [T&M Cost]
			        )
			    )
			
			```
		displayFolder: 90 Debug\Backups
		lineageTag: 8e17ae8c-acc5-4a45-aefa-f81afbaf3a03

	measure 'Gross Profit (By Stream) - Original' = ```
			
			        [Revenue (By Stream)] - [Cost (By Stream)]
			```
		displayFolder: 90 Debug\Backups
		lineageTag: aa31fe49-e53e-4918-9a93-071b5ec855f6

	measure 'GP % (By Stream) - Original' = ```
			
			        DIVIDE (
			            [Gross Profit (By Stream)],
			            [Revenue (By Stream)]
			        )
			```
		displayFolder: 90 Debug\Backups
		lineageTag: 4b0bbfec-a4da-483b-bfad-2988ef417375

	measure 'Customer Dashboard Title' = "Customer Dashboard - " & [Selected Customer Name]
		displayFolder: 90 Debug\UI
		lineageTag: 25d68af0-43d2-47a9-b3a0-21df595cb1a4

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Total Gross Profit (R12M As Of)' =
			VAR AnchorDate = [As Of Month End (Financials)]
			RETURN
			    IF (
			        ISBLANK ( AnchorDate ),
			        BLANK (),
			        CALCULATE (
			            [Total Gross Profit],
			            DATESINPERIOD ( FYCALENDAR_D[Date], AnchorDate, -12, MONTH )
			        )
			    )
		displayFolder: 05 Time Intelligence – R12M\Gross Profit
		lineageTag: 6d8bebf2-ad20-42cf-9c99-4c2f801a9dcd

	measure 'Total Gross Profit PY (R12M As Of)' =
			VAR AnchorDate = [As Of Month End (Financials)]
			VAR AnchorPY = EDATE ( AnchorDate, -12 )
			RETURN
			    IF (
			        ISBLANK ( AnchorDate ),
			        BLANK (),
			        CALCULATE (
			            [Total Gross Profit],
			            DATESINPERIOD ( FYCALENDAR_D[Date], AnchorPY, -12, MONTH )
			        )
			    )
		displayFolder: 05 Time Intelligence – R12M\Gross Profit
		lineageTag: 24e0f082-65d8-4455-ae1a-29cf9ed309ec

	measure 'Total GP % (R12M As Of)' = DIVIDE( [Total Gross Profit (R12M As Of)], [Total Revenue (R12M As Of)] )
		formatString: 0.0%;-0.0%;0.0%
		displayFolder: 05 Time Intelligence – R12M\GP %
		lineageTag: bf05e9d2-03fd-4e1e-a296-5e276c9a925d

	measure 'Total GP % PY (R12M As Of)' = DIVIDE( [Total Gross Profit PY (R12M As Of)], [Total Revenue PY (R12M As Of)] )
		formatString: 0.0%;-0.0%;0.0%
		displayFolder: 05 Time Intelligence – R12M\GP %
		lineageTag: 8cbb0b58-1ad8-4c16-aec7-adc312dac6e0

	measure 'Total GP % Trend vs PY (R12M As Of)' = ```
			
			VAR CY = [Total GP % (R12M As Of)]
			VAR PY = [Total GP % PY (R12M As Of)]
			
			RETURN
			IF( 
			    ISBLANK(CY) || CY = 0 || ISBLANK(PY) || PY = 0, 
			    BLANK(),
			    VAR Diff = CY - PY
			    VAR Icon = 
			        SWITCH( TRUE(),
			            Diff > 0, UNICHAR(9650), -- ▲
			            Diff < 0, UNICHAR(9660), -- ▼
			            UNICHAR(9644)            -- ▬
			        )
			    VAR PctText = 
			        IF( Diff >= 0, 
			            UNICHAR(8199) & FORMAT( Diff, "0.0%" ), 
			            FORMAT( Diff, "0.0%" )
			        )
			    RETURN
			        Icon & " " & PctText
			)
			
			```
		displayFolder: 05 Time Intelligence – R12M\GP %
		lineageTag: 6a25ddbf-7cdd-4690-b723-46b17562e6b5

	measure 'Customer Lifecycle Status Color' = ```
			
			VAR MonthsInactive = [Months Since Last Billing]
			RETURN
			    SWITCH (
			        TRUE (),
			        ISBLANK ( MonthsInactive ), "#BDBDBD", -- No Activity
			        MonthsInactive < 6,         "#7BAF7D", -- Active
			        MonthsInactive < 12,        "#EBC06D", -- Dormant
			        "#D17A73"                              -- Inactive
			    )
			
			```
		displayFolder: 08 Customer Status\Helpers
		lineageTag: fe0a800f-d2b8-4260-994f-63716a5e7608

	measure 'Total Revenue Trend vs PY (R12M As Of)' = ```
			
			VAR CY = [Total Revenue (R12M As Of)]
			VAR PY = [Total Revenue PY (R12M As Of)]
			
			RETURN
			IF( 
			    (ISBLANK(CY) || CY = 0) && (ISBLANK(PY) || PY = 0), 
			    BLANK(),
			    VAR PctChange = DIVIDE( CY - PY, PY )
			    VAR Icon = 
			        SWITCH( TRUE(),
			            PctChange > 0, UNICHAR(9650), -- ▲
			            PctChange < 0, UNICHAR(9660), -- ▼
			            UNICHAR(9644)                 -- ▬
			        )
			    VAR PctText = 
			        IF( PctChange >= 0, 
			            UNICHAR(8199) & FORMAT( PctChange, "0.0%" ), 
			            FORMAT( PctChange, "0.0%" )
			        )
			    RETURN
			        Icon & " " & PctText
			)
			
			```
		displayFolder: 05 Time Intelligence – R12M\Revenue
		lineageTag: 63b5b485-20fc-4ada-aadd-dec640e909de

	measure 'Total Gross Profit Trend vs PY (R12M As Of)' = ```
			
			VAR CY = [Total Gross Profit (R12M As Of)]
			VAR PY = [Total Gross Profit PY (R12M As Of)]
			
			RETURN
			IF( 
			    (ISBLANK(CY) || CY = 0) && (ISBLANK(PY) || PY = 0), 
			    BLANK(),
			    VAR PctChange = DIVIDE( CY - PY, PY )
			    VAR Icon = 
			        SWITCH( TRUE(),
			            PctChange > 0, UNICHAR(9650), -- ▲
			            PctChange < 0, UNICHAR(9660), -- ▼
			            UNICHAR(9644)                 -- ▬
			        )
			    VAR PctText = 
			        IF( PctChange >= 0, 
			            UNICHAR(8199) & FORMAT( PctChange, "0.0%" ), 
			            FORMAT( PctChange, "0.0%" )
			        )
			    RETURN
			        Icon & " " & PctText
			)
			
			```
		displayFolder: 05 Time Intelligence – R12M\Gross Profit
		lineageTag: d8478713-b516-4954-a3aa-b745c61a373d

	measure 'Customer Revenue Rank (All Time)' = ```
			
			RANKX(
			    ALL( CUSTOMERS_D ),
			    [Total Revenue],
			    ,
			    DESC,
			    Dense
			)
			
			```
		displayFolder: 08 Customer Status\Profile
		lineageTag: e3ac8f3a-9a4f-48d1-b717-c58c63863b08

	measure 'Customer Rank Context String' = ```
			
			VAR RankVal = [Customer Revenue Rank (All Time)]
			VAR TotalCustomers = 
			    CALCULATE( 
			        COUNTROWS(CUSTOMERS_D), 
			        FILTER( ALL(CUSTOMERS_D), [Total Revenue] > 0 ) 
			    )
			RETURN
			    IF( 
			        ISBLANK([Total Revenue]), 
			        "No Revenue",
			        "#" & FORMAT(RankVal, "#,0") & " of " & FORMAT(TotalCustomers, "#,0")
			    )
			
			```
		displayFolder: 08 Customer Status\Profile
		lineageTag: 67782e7e-f746-4b7f-9dc6-d76bc06648fc

	measure 'Customer Profile Metric Value' = ```
			
			VAR _SelectedMetric = SELECTEDVALUE( 'UI_CustomerProfileRows'[Metric Name] )
			VAR _Result = 
			    SWITCH( _SelectedMetric,
			        "Lifecycle Status",  [Customer Lifecycle Status],
			        "Last Billing Date", FORMAT( [Last Billing Date (Any Stream)], "MMM d, yyyy" ),
			        "Active Contracts",  FORMAT( [Active Contracts], "#,0" ),
			        "Active Projects",   FORMAT( [Active Projects], "#,0" ),
			        "Lifetime Revenue",  FORMAT( [Total Revenue], "$#,0" ),
			        "Lifetime GP",       FORMAT( [Total Gross Profit], "$#,0" ),
			        "Lifetime GP %",     FORMAT( [Total GP %], "0.0%" ),
			        "Revenue Rank",      [Customer Rank Context String]
			    )
			RETURN
			    IF( ISBLANK(_Result), "--", _Result )
			
			```
		displayFolder: 08 Customer Status\Profile
		lineageTag: 23f8dd6b-9d0d-4491-8c0d-8d545574dcfc

	measure 'Contract Booking Value (Active Only)' = ```
			
			CALCULATE(
			    SUM( 'SERVCONTRACTS_D'[Contract Amount] ),
			    'SERVCONTRACTS_D'[IsActiveContract] = TRUE
			)
			
			```
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 08 Customer Status\Contract
		lineageTag: 64e4fb8a-79e7-4550-befa-395c80529f70

	measure 'Is In Top N (Active Contracts Value)' = ```
			
			VAR N = SELECTEDVALUE ( 'Top N'[Top N], 10 )
			VAR RankVal = 
			    RANKX( 
			        ALLSELECTED( 'SERVCONTRACTS_D'[Contract Number] ), 
			        [Contract Booking Value (Active Only)], 
			        , 
			        DESC, 
			        Dense 
			    )
			RETURN
			    IF ( NOT ISBLANK([Contract Booking Value (Active Only)]) && RankVal <= N, 1, 0 )
			
			```
		displayFolder: 08 Customer Status\Contract
		lineageTag: 3c6762a7-745a-4085-9937-7ba903bf3f14

	measure 'NPS Response Count' = ```
			
			COUNTROWS( NPS_F )
			
			```
		formatString: #,0
		displayFolder: 10 Customer Experience
		lineageTag: 5ce620fd-4ba7-41be-b468-00eddf1b1e9b

	measure 'Promoters Count' = ```
			
			CALCULATE( COUNTROWS(NPS_F), NPS_F[1-10 Score] >= 9 )
			
			```
		formatString: #,0
		displayFolder: 10 Customer Experience
		lineageTag: c79a9a8e-78d6-4a26-8260-be5789d72fe9

	measure 'Detractors Count' = ```
			
			CALCULATE( COUNTROWS(NPS_F), NPS_F[1-10 Score] <= 6 )
			
			```
		formatString: #,0
		displayFolder: 10 Customer Experience
		lineageTag: ca8982f1-0603-4305-adee-6ea9e1d098dd

	measure 'Net Promoter Score' = ```
			
			VAR P = [Promoters Count]
			VAR D = [Detractors Count]
			VAR Total = [NPS Response Count]
			RETURN
			    IF( Total > 0, DIVIDE( P - D, Total ) * 100 )
			
			```
		formatString: 0.0
		displayFolder: 10 Customer Experience
		lineageTag: 71f4627b-6f9d-4b20-b78c-9d35174227fc

	measure 'Prompt and On Time %' = ```
			
			VAR _Yes = CALCULATE( COUNTROWS(NPS_F), NPS_F[Prompt and On Time] = "Yes" )
			VAR _No  = CALCULATE( COUNTROWS(NPS_F), NPS_F[Prompt and On Time] = "No" )
			RETURN
			    DIVIDE( _Yes, _Yes + _No )
			
			```
		formatString: 0.0%
		displayFolder: 10 Customer Experience
		lineageTag: b23b7723-ccb6-46c2-a184-d656e56c6ac7

	measure 'First Time Fix Rate' = ```
			
			VAR _Yes = CALCULATE( COUNTROWS(NPS_F), NPS_F[First Time Fix] = "Yes" )
			VAR _No  = CALCULATE( COUNTROWS(NPS_F), NPS_F[First Time Fix] = "No" )
			RETURN
			    DIVIDE( _Yes, _Yes + _No )
			
			```
		formatString: 0.0%
		displayFolder: 10 Customer Experience
		lineageTag: 2d9991c4-3f01-442a-b95c-d237f82cbb57

	measure 'Net Promoter Score (R12M)' = ```
			
			VAR MaxVisible = MAX('FYCALENDAR_D'[Date])
			VAR LastData = [Last NPS Date]
			VAR AnchorDate = MIN( MaxVisible, LastData )
			RETURN
			    CALCULATE(
			        [Net Promoter Score],
			        DATESINPERIOD( 'FYCALENDAR_D'[Date], AnchorDate, -12, MONTH ),
			        REMOVEFILTERS( 'FYCALENDAR_D' )
			    )
			
			```
		formatString: 0.0
		displayFolder: 10 Customer Experience\R12M
		lineageTag: 1b74a704-a8c5-4288-876b-0a81f1cf3d42

	measure 'Net Promoter Score PY (R12M)' = ```
			
			VAR MaxVisible = MAX('FYCALENDAR_D'[Date])
			VAR LastData = [Last NPS Date]
			VAR AnchorDate = MIN( MaxVisible, LastData )
			VAR AnchorPY = EDATE(AnchorDate, -12)
			RETURN
			    CALCULATE(
			        [Net Promoter Score],
			        DATESINPERIOD( 'FYCALENDAR_D'[Date], AnchorPY, -12, MONTH ),
			        REMOVEFILTERS( 'FYCALENDAR_D' )
			    )
			
			```
		formatString: 0.0
		displayFolder: 10 Customer Experience\R12M
		lineageTag: 952a82cf-55ec-4cf8-8b81-ef9411405af1

	measure 'Net Promoter Score Trend vs PY (R12M)' = ```
			
			VAR CY = [Net Promoter Score (R12M)]
			VAR PY = [Net Promoter Score PY (R12M)]
			
			RETURN
			IF( 
			    ISBLANK(CY) || CY = 0 || ISBLANK(PY) || PY = 0, 
			    BLANK(),
			    VAR Diff = CY - PY
			    VAR Icon = 
			        SWITCH( TRUE(),
			            Diff > 0, UNICHAR(9650), -- ▲
			            Diff < 0, UNICHAR(9660), -- ▼
			            UNICHAR(9644)            -- ▬
			        )
			    VAR PctText = 
			        IF( Diff >= 0, 
			            UNICHAR(8199) & FORMAT( Diff, "0.0" ), 
			            FORMAT( Diff, "0.0" )
			        )
			    RETURN
			        Icon & " " & PctText
			)
			
			```
		displayFolder: 10 Customer Experience\R12M
		lineageTag: bedb4cb7-48b0-4cd5-9268-007a6efe6949

	measure 'Debug - R12M Start Date' = ```
			
			VAR AnchorDate = [As Of Month End (SC)]
			RETURN
			    CALCULATE(
			        MIN( FYCALENDAR_D[Date] ),
			        DATESINPERIOD( FYCALENDAR_D[Date], AnchorDate, -12, MONTH )
			    )
			
			```
		formatString: Short Date
		displayFolder: 90 Debug
		lineageTag: 7ce36e9c-56ca-4c4b-8ab0-2a6a50912377

	measure 'Debug - R12M End Date' = ```
			
			VAR AnchorDate = [As Of Month End (SC)]
			RETURN
			    CALCULATE(
			        MAX( FYCALENDAR_D[Date] ),
			        DATESINPERIOD( FYCALENDAR_D[Date], AnchorDate, -12, MONTH )
			    )
			
			```
		formatString: Short Date
		displayFolder: 90 Debug
		lineageTag: 75c4268d-2472-40bb-bc5d-2014e8967767

	measure 'Last NPS Date' = ```
			
			CALCULATE( MAX(NPS_F[SERVICE CALL COMPLETED DATE]), REMOVEFILTERS('FYCALENDAR_D') )
			
			```
		displayFolder: 10 Customer Experience
		lineageTag: 3141a36d-a696-4a2a-bf51-3c014fce6e17

	measure 'Debug - NPS R12M Start Date' = ```
			
			VAR MaxVisible = MAX('FYCALENDAR_D'[Date])
			VAR LastData = [Last NPS Date]
			VAR AnchorDate = MIN( MaxVisible, LastData )
			RETURN
			    CALCULATE(
			        MIN('FYCALENDAR_D'[Date]),
			        DATESINPERIOD( 'FYCALENDAR_D'[Date], AnchorDate, -12, MONTH ),
			        REMOVEFILTERS( 'FYCALENDAR_D' )
			    )
			
			```
		formatString: Short Date
		displayFolder: 90 Debug
		lineageTag: ee11ec12-b128-4a3e-8ef3-2ff38597f9af

	measure 'Debug - NPS R12M End Date' = ```
			
			VAR MaxVisible = MAX('FYCALENDAR_D'[Date])
			VAR LastData = [Last NPS Date]
			VAR AnchorDate = MIN( MaxVisible, LastData )
			RETURN
			    CALCULATE(
			        MAX('FYCALENDAR_D'[Date]),
			        DATESINPERIOD( 'FYCALENDAR_D'[Date], AnchorDate, -12, MONTH ),
			        REMOVEFILTERS( 'FYCALENDAR_D' )
			    )
			
			```
		formatString: Short Date
		displayFolder: 90 Debug
		lineageTag: 2b3f0cc7-10d1-4da3-ad46-4433d3d10f4a

	measure 'Net Promoter Score PY' = ```
			
			CALCULATE(
			    [Net Promoter Score],
			    SAMEPERIODLASTYEAR( 'FYCALENDAR_D'[Date] )
			)
			
			```
		formatString: 0.0
		displayFolder: 10 Customer Experience
		lineageTag: 291ea2a5-d298-4e6e-8da5-be8ec692e1e0

	measure 'Net Promoter Score Trend vs PY' = ```
			
			VAR CY = [Net Promoter Score]
			VAR PY = [Net Promoter Score PY]
			
			RETURN
			IF( 
			    ISBLANK(CY) || ISBLANK(PY), 
			    BLANK(),
			    VAR Diff = CY - PY
			    VAR Icon = 
			        SWITCH( TRUE(),
			            Diff > 0, UNICHAR(9650), -- ▲
			            Diff < 0, UNICHAR(9660), -- ▼
			            UNICHAR(9644)            -- ▬
			        )
			    VAR PctText = 
			        IF( Diff >= 0, 
			            UNICHAR(8199) & FORMAT( Diff, "0.0" ), 
			            FORMAT( Diff, "0.0" )
			        )
			    RETURN
			        Icon & " " & PctText
			)
			
			```
		displayFolder: 10 Customer Experience
		lineageTag: aac2a233-6e35-4338-bdb5-3a993f2063e1

	measure 'Revenue Δ vs PY Data Bar' = [Total Revenue YoY % (FY As Of)]
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: 01 Revenue\By Stream
		lineageTag: f0a8d56e-6d50-4324-81bb-65aad223906a

	measure 'Total Revenue PY' = ```
			
			CALCULATE(
			    [Total Revenue],
			    DATEADD( 'FYCALENDAR_D'[Date], -1, YEAR )
			)
			
			```
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 04 Time Intelligence – Fiscal\Revenue
		lineageTag: 9ec22c72-6917-4518-a142-293e1c5436f9

	/// Standard Period Comparison. Compares the selected period (Month/Quarter) to the same period last year. Use for Charts and Trend Tables.
	measure 'Total GP % Trend vs PY' = ```
			
			VAR CY = [Total GP %]
			VAR PY = CALCULATE( [Total GP %], SAMEPERIODLASTYEAR('FYCALENDAR_D'[Date]) )
			RETURN
			IF( 
			    ISBLANK(CY) || CY = 0, 
			    BLANK(),
			    VAR Diff = CY - PY
			    VAR Icon = 
			        SWITCH( TRUE(),
			            Diff > 0, UNICHAR(9650), -- ▲
			            Diff < 0, UNICHAR(9660), -- ▼
			            UNICHAR(9644)            -- ▬
			        )
			    VAR PctText = 
			        IF( Diff >= 0, 
			            UNICHAR(8199) & FORMAT( Diff, "0.0%" ), 
			            FORMAT( Diff, "0.0%" )
			        )
			    RETURN
			        Icon & " " & PctText
			)
			
			```
		displayFolder: 03 Gross Profit\Total
		lineageTag: e2123dec-0736-4698-a307-43777c82f655

	measure 'Reporting Period Subtitle' = ```
			
			VAR SelYear = [Selected Fiscal Year (Single)]
			VAR AsOfDate = [As Of Date (Financials)]
			VAR IsCurrent = [Selected FY Is Current] = 1
			
			RETURN
			    IF( 
			        ISBLANK(SelYear),
			        "Select a Fiscal Year",
			        IF( 
			            IsCurrent,
			            "FY " & SelYear & " YTD (Data through " & FORMAT( AsOfDate, "MMM d, yyyy" ) & ")",
			            "Fiscal Year " & SelYear & " (Full Year)"
			        )
			    )
			
			```
		displayFolder: 90 Debug\UI
		lineageTag: 288ad653-13fa-4010-9439-18df9d2c0665

	measure 'Revenue PY (By Stream)' = ```
			
			CALCULATE(
			    [Revenue (By Stream)],
			    DATEADD( 'FYCALENDAR_D'[Date], -1, YEAR )
			)
			
			```
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 01 Revenue\By Stream
		lineageTag: ee7d9924-94e8-4fbd-9fa0-b5521e0a1681

	measure 'Revenue Cumulative (By Stream)' = ```
			
			IF( 
			    SELECTEDVALUE('FYCALENDAR_D'[Is Completed Month]) = TRUE,
			    TOTALYTD(
			        [Revenue (By Stream)],
			        'FYCALENDAR_D'[Date],
			        "09-30"
			    ),
			    BLANK()
			)
			
			```
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 01 Revenue\By Stream
		lineageTag: e48373e5-11a4-4dcb-a420-37e430351802

	measure 'Revenue Cumulative (By Stream) PY' = ```
			
			CALCULATE(
			    [Revenue Cumulative (By Stream)],
			    DATEADD( 'FYCALENDAR_D'[Date], -1, YEAR )
			)
			
			```
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 01 Revenue\By Stream
		lineageTag: bc2f3eb8-0949-4636-821f-277083e65ada

	measure 'Revenue PY (Dynamic)' = ```
			
			VAR SelectionOrder = MAX( 'UI_Revenue View Mode'[Revenue View Mode Order] ) -- Use MAX to avoid error if multiple selected (defaults to one)
			RETURN
			    IF( 
			        SelectionOrder = 1, 
			        [Revenue Cumulative (By Stream) PY],
			        [Revenue PY (By Stream)]
			    )
			
			```
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 01 Revenue\By Stream
		lineageTag: 7bf79e80-0be3-47be-9fb3-8988eb73ddbe

	measure 'Churn Revenue % (R12M As Of)' = DIVIDE( [Churn Revenue (R12M As Of)], [Retention Base PY (R12M As Of)], 0 )
		formatString: 0.0%;-0.0%;0.0%
		displayFolder: 07 Retention KPIs – R12M As Of\NRR Components
		lineageTag: 378c6f29-4319-4a28-92b2-0627150edb2c

	measure 'Contraction Revenue % (R12M As Of)' = DIVIDE( [Contraction Revenue (R12M As Of)], [Retention Base PY (R12M As Of)], 0 )
		formatString: 0.0%;-0.0%;0.0%
		displayFolder: 07 Retention KPIs – R12M As Of\NRR Components
		lineageTag: 2cb2ca6b-918c-4ef7-9279-65e80f978fa0

	measure 'Upsell Revenue % (R12M As Of)' = DIVIDE( [Upsell Revenue (R12M As Of)], [Retention Base PY (R12M As Of)], 0 )
		formatString: 0.0%;-0.0%;0.0%
		displayFolder: 07 Retention KPIs – R12M As Of\NRR Components
		lineageTag: 745a2475-091b-4526-9f6c-ff7d863a625b

	measure 'Logo Churn % (SC) (R12M As Of)' = DIVIDE( [Logo Churn Count (SC) (R12M As Of)], [Retention Base Count PY (SC) (R12M As Of)], 0 )
		formatString: 0.0%;-0.0%;0.0%
		displayFolder: 07 Retention KPIs – R12M As Of\Logo Retention (SC)
		lineageTag: 0af86e4f-0656-4b4c-a7f2-2d4758865120

	measure 'NRR Waterfall Value' = ```
			VAR Selection = SELECTEDVALUE ( 'Waterfall - NRR Breakdown'[ID] )
			RETURN
			    SWITCH (
			        Selection,
			        1, [Retention Base PY (R12M As Of)],
			        2, -1 * [Churn Revenue (R12M As Of)],        
			        3, -1 * [Contraction Revenue (R12M As Of)],  
			        4, [Upsell Revenue (R12M As Of)],
			        5, [Retention Base PY (R12M As Of)] 
			           - [Churn Revenue (R12M As Of)] 
			           - [Contraction Revenue (R12M As Of)] 
			           + [Upsell Revenue (R12M As Of)]            
			    )
			```
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: 07 Retention KPIs – R12M As Of\NRR Components
		lineageTag: aa71f600-3a00-4fa5-b728-7da825f19aba

	measure 'Customer Retention Category (R12M)' =
			VAR RevCY = [SC Revenue (R12M As Of)]
			VAR RevPY = [SC Revenue PY (R12M As Of)]
			RETURN
			    SWITCH (
			        TRUE (),
			        RevPY > 0 && (ISBLANK(RevCY) || RevCY = 0), "Churned",
			        RevPY > 0 && RevCY < RevPY, "Contraction",
			        RevPY > 0 && RevCY > RevPY, "Upsell",
			        RevPY > 0 && RevCY = RevPY, "Steady",
			        RevPY = 0 && RevCY > 0, "New Business",
			        BLANK()
			    )
		displayFolder: 07 Retention KPIs – R12M As Of\NRR Components
		lineageTag: 9406eb75-b194-4825-b904-ac671780cc85

	measure 'R12M Period Label' = ```
			VAR EndDate = [As Of Month End (SC)]
			VAR StartDate = EDATE( EndDate, -11 )
			RETURN
			    IF ( 
			        ISBLANK( EndDate ), 
			        "No Data",
			        FORMAT( StartDate, "MMM yyyy" ) & " – " & FORMAT( EndDate, "MMM yyyy" )
			    )
			```
		displayFolder: 05 Time Intelligence – R12M\Helpers
		lineageTag: 25744551-49f4-4923-9247-80bff57107de

	measure 'NRR Logic Explanation.MD' =
			" # Net Revenue Retention (NRR) Logic Explained
			
			This document explains the logic behind the **Net Revenue Retention (NRR)** calculations in the Power BI report, specifically focusing on how customers are classified as ""Churned"" versus ""Lapsed.""
			
			## 1. The ""As Of"" Anchor Date
			Every calculation starts with a single **Anchor Date**. This is the reference point for ""Today"" in the report.
			
			*   **Logic:** The report looks at your Date Slicer selection and snaps to the **latest available Service Contract data**.
			*   **Example:** If you select ""FY2026"" but data only exists through **December 2025**, the Anchor Date becomes **Dec 31, 2025**.
			
			## 2. The Two Comparison Windows
			Once the Anchor Date is set, the system looks at two specific 12-month periods:
			
			1.  **Current Period (CY):** The 12 months ending on the Anchor Date.
			    *   *Example:* Jan 1, 2025 – Dec 31, 2025
			2.  **Prior Period (PY):** The 12 months *before* the Current Period.
			    *   *Example:* Jan 1, 2024 – Dec 31, 2024
			
			## 3. How Status is Determined (The ""Who Counts?"" Rule)
			NRR only measures how well we retained customers who were **already with us** in the Prior Period.
			
			| Customer Scenario | Revenue in Prior Period (PY) | Revenue in Current Period (CY) | Status | Impact on NRR |
			| :--- | :---: | :---: | :--- | :--- |
			| **Steady** | ✅ Yes ($10k) | ✅ Yes ($10k) | **Steady** | Neutral (100%) |
			| **Growth** | ✅ Yes ($10k) | ✅ Yes ($15k) | **Upsell** | Positive (>100%) |
			| **Decline** | ✅ Yes ($10k) | ✅ Yes ($5k) | **Contraction** | Negative (<100%) |
			| **Lost Recently** | ✅ Yes ($10k) | ❌ No ($0) | **Churned** | Negative (0%) |
			| **Lost Long Ago** | ❌ No ($0) | ❌ No ($0) | **Lapsed** | **Excluded** |
			| **New Customer** | ❌ No ($0) | ✅ Yes ($10k) | **New Business** | **Excluded** |
			
			---
			
			## 4. The ""Churn vs. Lapsed"" Cliff (Example)
			*Why does a customer count as Churn one month, but disappear the next?*
			
			Imagine **Anchor Date = Dec 31, 2025**.
			*   **Prior Period (PY):** Jan 2024 – Dec 2024.
			
			### Scenario A: Last Bill was Jan 1, 2024
			*   **Is Jan 1, 2024 in the Prior Period?** Yes.
			*   **Result:** This customer counts as **Base Revenue**.
			*   **Current Status:** Since they have paid nothing since, they are marked as **Churned**.
			*   **Impact:** They hurt the NRR score.
			
			### Scenario B: Last Bill was Dec 1, 2023
			*   **Is Dec 1, 2023 in the Prior Period?** No. (It is before Jan 2024).
			*   **Result:** This customer had **$0 revenue** in the Prior Period (Jan–Dec 2024).
			*   **Current Status:** **Lapsed / Inactive**.
			*   **Impact:** They are **excluded** from the calculation entirely. We already ""counted"" their churn in last year's report; we don't count it again this year.
			"
		displayFolder: 90 Debug
		lineageTag: 8ca1f05f-7d7d-41e9-8f04-fab0d7732d9b

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'NRR % PY Display (R12M)' = ```
			VAR AnchorDate = [As Of Month End (SC)]
			VAR FirstBill = [First Contract Billing Date]
			VAR MonthsSinceStart = DATEDIFF( FirstBill, AnchorDate, MONTH )
			
			RETURN
			    IF ( 
			        HASONEVALUE( CUSTOMERS_D[Customer Name] ), -- Changed from ISINSCOPE
			        SWITCH (
			            TRUE (),
			            MonthsSinceStart < 12, "New",
			            MonthsSinceStart < 36, "Ramp Up",
			            FORMAT( [Net Revenue Retention % PY (R12M As Of)], "0.0%" )
			        ),
			        FORMAT( [Net Revenue Retention % PY (R12M As Of)], "0.0%" )
			    )
			```
		displayFolder: 07 Retention KPIs – R12M As Of\NRR
		lineageTag: 3c6d0b5e-bfd3-4497-ac0f-4a437880b590

	measure 'NRR Trend Display (R12M)' = ```
			VAR AnchorDate = [As Of Month End (SC)]
			VAR FirstBill = [First Contract Billing Date]
			VAR MonthsSinceStart = DATEDIFF( FirstBill, AnchorDate, MONTH )
			
			RETURN
			    IF ( 
			        HASONEVALUE( CUSTOMERS_D[Customer Name] ) && MonthsSinceStart < 36, -- Changed from ISINSCOPE
			        BLANK(), 
			        [Net Revenue Retention Trend vs PY (R12M As Of)]
			    )
			```
		displayFolder: 07 Retention KPIs – R12M As Of\NRR
		lineageTag: 541ddab0-c395-4638-a4d7-ddf448e24909

	measure 'Financials Max Allowed Month End' = EOMONTH ( TODAY(), -1 )
		displayFolder: 05 Time Intelligence – R12M\Helpers

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'As Of Month End (Financials)' =
			VAR MaxAllowedMonthEnd =
			    [Financials Max Allowed Month End]
			VAR LastBillingDate =
			    CALCULATE (
			        MAX ( ALL_BILLINGS_F[BillingDate] ),
			        REMOVEFILTERS (),
			        ALL_BILLINGS_F[Billing Amount] <> 0,
			        ALL_BILLINGS_F[BillingDate] <= MaxAllowedMonthEnd
			    )
			RETURN
			    IF ( ISBLANK ( LastBillingDate ), BLANK (), EOMONTH ( LastBillingDate, 0 ) )
		displayFolder: 05 Time Intelligence – R12M\Helpers

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Revenue Stream Share Fill (R12M)' =
			VAR AxisStream =
			    SELECTEDVALUE ( Revenue_Stream_D[Revenue Stream] )
			VAR Part =
			    SELECTEDVALUE ( UI_BarFillParts[Part] )
			VAR TotalRev =
			    [Total Revenue (R12M As Of)]
			VAR ProjectRev =
			    [Project Revenue (R12M As Of)]
			VAR TMRev =
			    [T&M Revenue (R12M As Of)]
			VAR SCRev =
			    TotalRev - ProjectRev - TMRev
			VAR StreamRev =
			    SWITCH ( AxisStream, "Project", ProjectRev, "Service Contract", SCRev, "T&M", TMRev )
			RETURN
			    SWITCH (
			        TRUE (),
			        ISBLANK ( AxisStream ) || ISBLANK ( Part ) || ISBLANK ( TotalRev ) || TotalRev = 0,
			            BLANK (),
			        Part = "Remainder",
			            MAX ( 0, TotalRev - StreamRev ),
			        Part = AxisStream,
			            StreamRev,
			        0
			    )
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: UI\Customer\Summary by Stream (R12M)

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Revenue (R12M As Of) (By Stream)' =
			VAR AxisStream =
			    SELECTEDVALUE ( Revenue_Stream_D[Revenue Stream] )
			VAR TotalRev =
			    [Total Revenue (R12M As Of)]
			VAR ProjectRev =
			    [Project Revenue (R12M As Of)]
			VAR TMRev =
			    [T&M Revenue (R12M As Of)]
			VAR SCRev =
			    TotalRev - ProjectRev - TMRev
			RETURN
			    SWITCH ( AxisStream, "Project", ProjectRev, "Service Contract", SCRev, "T&M", TMRev )
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: UI\Customer\Summary by Stream (R12M)

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'Recurring Mix % (SC) (R12M)' =
			VAR TotalRev = [Total Revenue (R12M As Of)]
			VAR ProjectRev = [Project Revenue (R12M As Of)]
			VAR TMRev = [T&M Revenue (R12M As Of)]
			VAR SCRev = TotalRev - ProjectRev - TMRev
			RETURN DIVIDE ( SCRev, TotalRev )
		formatString: 0.0%;-0.0%;0.0%
		displayFolder: UI\Customer\Risk & Opportunity (R12M)

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Stream Concentration % (R12M)' =
			VAR TotalRev = [Total Revenue (R12M As Of)]
			VAR ProjectRev = [Project Revenue (R12M As Of)]
			VAR TMRev = [T&M Revenue (R12M As Of)]
			VAR SCRev = TotalRev - ProjectRev - TMRev
			VAR TopStreamRev = MAX ( ProjectRev, MAX ( SCRev, TMRev ) )
			RETURN DIVIDE ( TopStreamRev, TotalRev )
		formatString: 0.0%;-0.0%;0.0%
		displayFolder: UI\Customer\Risk & Opportunity (R12M)

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Revenue Momentum % (R12M vs PY)' =
			VAR CY = [Total Revenue (R12M As Of)]
			VAR PY = [Total Revenue PY (R12M As Of)]
			RETURN IF ( ISBLANK ( PY ) || PY = 0, BLANK (), DIVIDE ( CY - PY, PY ) )
		formatString: 0.0%;-0.0%;0.0%
		displayFolder: UI\Customer\Risk & Opportunity (R12M)

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	column _Dummy
		isHidden
		lineageTag: 5a8e02fc-c19f-452b-a41d-40d5b55a211f
		isNameInferred
		sourceColumn: [_Dummy]

	partition '_Key Measures' = calculated
		mode: import
		source = DATATABLE("_Dummy", STRING, {{"Measure Container"}})

